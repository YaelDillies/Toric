/-
Copyright (c) 2025 YaÃ«l Dillies, MichaÅ‚ MrugaÅ‚a, Yunzhou Xie. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, MichaÅ‚ MrugaÅ‚a, Yunzhou Xie
-/
import Mathlib.Algebra.Algebra.Bilinear
import Mathlib.LinearAlgebra.TensorProduct.Tower
import Toric.Mathlib.RingTheory.Coalgebra.Basic
import Toric.Mathlib.RingTheory.Coalgebra.CoassocSimps

/-!
# Convolution product on linear maps from a coalgebra to an algebra

This file constructs the ring structure on linear maps `C â†’ A` where `C` is a coalgebra and `A` an
algebra, where multiplication is given by
```
         .
        / \
f * g = f g
        \ /
         .
```
-/

suppress_compilation

open Coalgebra TensorProduct

variable {R A B C : Type*} [CommSemiring R]

namespace LinearMap

local notation "Î·" => Algebra.linearMap R A
local notation "Îµ" => counit (R := R) (A := C)
local notation "Î¼" => mul' R A
local notation "Î´" => comul
local infix:70 " âŠ—â‚˜ " => TensorProduct.map
-- local notation "Î±" => TensorProduct.assoc _ _ _

section Semiring
variable [Semiring A] [Semiring B] [AddCommMonoid C] [Algebra R A] [Algebra R B] [Module R C]
  [Coalgebra R C]

instance instOne : One (C â†’â‚—[R] A) where one := Algebra.linearMap R A âˆ˜â‚— counit
instance instMul : Mul (C â†’â‚—[R] A) where mul f g := Î¼ âˆ˜â‚— TensorProduct.map f g âˆ˜â‚— comul

lemma one_def : (1 : C â†’â‚—[R] A) = Algebra.linearMap R A âˆ˜â‚— counit := rfl
lemma mul_def (f g : C â†’â‚—[R] A) : f * g = Î¼ âˆ˜â‚— TensorProduct.map f g âˆ˜â‚— comul := rfl

@[simp] lemma one_apply (c : C) : (1 : C â†’â‚—[R] A) c = algebraMap R A (counit c) := rfl
@[simp] lemma mul_apply (f g : C â†’â‚—[R] A) (c : C) : (f * g) c = Î¼ (.map f g (comul c)) := rfl

lemma _root_.Coalgebra.Repr.mul_apply {a : C} (ğ“¡ : Coalgebra.Repr R a) (f g : C â†’â‚—[R] A) :
    (f * g) a = âˆ‘ i âˆˆ ğ“¡.index, f (ğ“¡.left i) * g (ğ“¡.right i) := by
  simp [mul_def, â† ğ“¡.eq]

private lemma convMul_assoc (f g h : C â†’â‚—[R] A) : f * g * h = f * (g * h) := by
  trans (((Î¼ âˆ˜â‚— (id âŠ—â‚˜ Î¼)) âˆ˜â‚— â†‘(TensorProduct.assoc R A A A)) âˆ˜â‚— ((f âŠ—â‚˜ g) âˆ˜â‚— Î´ âŠ—â‚˜ h)) âˆ˜â‚— Î´
  Â· simp only [mul_def, map_comp_left, â† LinearMap.comp_assoc]
    congr 3
    ext
    exact mul_assoc _ _ _
  Â· simp [mul_def, coassoc_simps]

private lemma one_convMul (f : C â†’â‚—[R] A) : 1 * f = f := calc
      Î¼ âˆ˜â‚— ((Î· âˆ˜â‚— Îµ) âŠ—â‚˜ f) âˆ˜â‚— Î´
  _ = Î¼ âˆ˜â‚— ((Î· âŠ—â‚˜ f) âˆ˜â‚— rTensor C Îµ) âˆ˜â‚— Î´  := by simp only [map_comp_rTensor]
  _ = Î¼ âˆ˜â‚— (Î· âŠ—â‚˜ f) âˆ˜â‚— (rTensor C Îµ) âˆ˜â‚— Î´ := by rw [comp_assoc]
  _ = Î¼ âˆ˜â‚— (rTensor A Î· âˆ˜â‚— lTensor R f) âˆ˜â‚— rTensor C Îµ âˆ˜â‚— Î´ := by simp
  _ = (Î¼ âˆ˜â‚— rTensor A Î·) âˆ˜â‚— lTensor R f âˆ˜â‚— (rTensor C Îµ âˆ˜â‚— Î´) := by simp only [comp_assoc]
  _ = (Î¼ âˆ˜â‚— rTensor A Î·) âˆ˜â‚— lTensor R f âˆ˜â‚— ((TensorProduct.mk R R C) 1) := by
    rw [rTensor_counit_comp_comul]
  _ = f := by ext; simp

private lemma convMul_one (f : C â†’â‚—[R] A) : f * 1 = f := calc
      Î¼ âˆ˜â‚— (f âŠ—â‚˜ (Î· âˆ˜â‚— Îµ)) âˆ˜â‚— Î´
  _ = Î¼ âˆ˜â‚— ((f âŠ—â‚˜ Î·) âˆ˜â‚— lTensor C Îµ) âˆ˜â‚— Î´  := by simp only [map_comp_lTensor]
  _ = Î¼ âˆ˜â‚— (f âŠ—â‚˜ Î·) âˆ˜â‚— lTensor C Îµ âˆ˜â‚— Î´ := by rw [comp_assoc]
  _ = Î¼ âˆ˜â‚— (lTensor A Î· âˆ˜â‚— rTensor R f) âˆ˜â‚— lTensor C Îµ âˆ˜â‚— Î´ := by simp
  _ = (Î¼ âˆ˜â‚— lTensor A Î·) âˆ˜â‚— rTensor R f âˆ˜â‚— (lTensor C Îµ âˆ˜â‚— Î´) := by simp only [comp_assoc]
  _ = (Î¼ âˆ˜â‚— lTensor A Î·) âˆ˜â‚— rTensor R f âˆ˜â‚— ((TensorProduct.mk R C R).flip 1) := by
    rw [lTensor_counit_comp_comul]
  _ = f := by ext; simp

instance : Semiring (C â†’â‚—[R] A) where
  left_distrib f g h := by ext; simp [TensorProduct.map_add_right]
  right_distrib f g h := by ext; simp [TensorProduct.map_add_left]
  zero_mul f := by ext; simp
  mul_zero f := by ext; simp
  mul_assoc := convMul_assoc
  one_mul := one_convMul
  mul_one := convMul_one

lemma _root_.AlgHom.map_comp_mul (h : A â†’â‚ B) :
    h.toLinearMap âˆ˜â‚— Î¼ = mul' R B âˆ˜â‚— (h.toLinearMap âŠ—â‚˜ h.toLinearMap) := by ext; simp

lemma comp_mul_distrib (f g : C â†’â‚—[R] A) (h : A â†’â‚[R] B) :
    h.toLinearMap.comp (f * g) = (.comp h.toLinearMap f) * (.comp h.toLinearMap g) := by
  simp only [mul_def, map_comp, â† comp_assoc, AlgHom.map_comp_mul]

end Semiring

section CommSemiring
variable [CommSemiring A] [AddCommMonoid C] [Algebra R A] [Module R C] [Coalgebra R C]
  [IsCocomm R C]

private lemma convMul_comm (f g : C â†’â‚—[R] A) : f * g = g * f := by
  rw [mul_def, â† comm_comp_comul, â† LinearMap.comp_assoc Î´, map_comp_comm_eq, mul_def,
    â† LinearMap.comp_assoc, â† LinearMap.comp_assoc, â† LinearMap.comp_assoc]
  congr 3
  ext; exact mul_comm _ _

instance : CommSemiring (C â†’â‚—[R] A) where
  mul_comm := convMul_comm

end CommSemiring

section Ring
variable [Ring A] [AddCommMonoid C] [Algebra R A] [Module R C] [Coalgebra R C]

instance : Ring (C â†’â‚—[R] A) where

end Ring

section CommRing
variable [CommRing A] [AddCommMonoid C] [Algebra R A] [Module R C] [Coalgebra R C] [IsCocomm R C]

instance : CommRing (C â†’â‚—[R] A) where

end CommRing
end LinearMap
