/-
Copyright (c) 2025 YaÃ«l Dillies, MichaÅ‚ MrugaÅ‚a, Yunzhou Xie. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, MichaÅ‚ MrugaÅ‚a, Yunzhou Xie
-/
import Mathlib.Algebra.Algebra.Bilinear
import Mathlib.LinearAlgebra.TensorProduct.Tower
import Mathlib.RingTheory.Coalgebra.Hom

/-!
# Convolution product on linear maps from a coalgebra to an algebra

This file constructs the ring structure on linear maps `C â†’ A` where `C` is a coalgebra and `A` an
algebra, where multiplication is given by `(f * g)(x) = âˆ‘ f xâ‚â‚â‚ * g xâ‚â‚‚â‚` in Sweedler notation or
```
         |
         Î¼
|   |   / \
f * g = f g
|   |   \ /
         Î´
         |
```
diagrammatically, where `Î¼` stands for multiplication and `Î´` for comultiplication.

## Implementation notes

Note that in the case `C = A` this convolution product conflicts with the (unfortunately global!)
group instance on `Module.End R A` with multiplication defined as composition.
As a result, the convolution product is scoped to `ConvolutionProduct`.
-/

@[inherit_doc] scoped[RingTheory.LinearMap] notation "Îµ" => CoalgebraStruct.counit
@[inherit_doc] scoped[RingTheory.LinearMap] notation "Î´" => CoalgebraStruct.comul
@[inherit_doc] scoped[RingTheory.LinearMap] notation "Î·" => Algebra.linearMap _ _
@[inherit_doc] scoped[RingTheory.LinearMap] notation "Î·[" R "]" => Algebra.linearMap R _
@[inherit_doc] scoped[RingTheory.LinearMap] notation "Î¼" => LinearMap.mul' _ _
@[inherit_doc] scoped[RingTheory.LinearMap] notation "Î¼[" R "]" => LinearMap.mul' R _
@[inherit_doc] scoped[RingTheory.LinearMap] infix:70 " âŠ—â‚˜ " => TensorProduct.map

namespace AlgHom
open scoped RingTheory.LinearMap
variable {R A B : Type*} [CommSemiring R] [Semiring A] [Semiring B] [Algebra R A] [Algebra R B]

lemma comp_mul' (f : A â†’â‚ B) : f.toLinearMap âˆ˜â‚— Î¼ = Î¼[R] âˆ˜â‚— (f.toLinearMap âŠ—â‚˜ f.toLinearMap) :=
  TensorProduct.ext' <| by simp

end AlgHom

suppress_compilation

open Coalgebra TensorProduct
open scoped RingTheory.LinearMap

variable {R A B C : Type*} [CommSemiring R]

namespace LinearMap
section NonUnitalNonAssocSemiring
variable
  [NonUnitalNonAssocSemiring A] [Module R A] [SMulCommClass R A A] [IsScalarTower R A A]
  [AddCommMonoid C] [Module R C] [CoalgebraStruct R C]

/-- Convolution product on linear maps from a coalgebra to an algebra. -/
abbrev convMul : Mul (C â†’â‚—[R] A) where mul f g := mul' R A âˆ˜â‚— TensorProduct.map f g âˆ˜â‚— comul

scoped[ConvolutionProduct] attribute [instance] LinearMap.convMul

open scoped ConvolutionProduct

lemma convMul_def (f g : C â†’â‚—[R] A) : f * g = mul' R A âˆ˜â‚— TensorProduct.map f g âˆ˜â‚— comul := rfl

@[simp]
lemma convMul_apply (f g : C â†’â‚—[R] A) (c : C) : (f * g) c = mul' R A (.map f g (comul c)) := rfl

lemma _root_.Coalgebra.Repr.convMul_apply {a : C} (ğ“¡ : Coalgebra.Repr R a) (f g : C â†’â‚—[R] A) :
    (f * g) a = âˆ‘ i âˆˆ ğ“¡.index, f (ğ“¡.left i) * g (ğ“¡.right i) := by
  simp [convMul_def, â† ğ“¡.eq]

/-- Non-unital and non-associative convolution semiring structure on linear maps from a
coalgebra to a non-unital non-associative algebra. -/
abbrev convNonUnitalNonAssocSemiring : NonUnitalNonAssocSemiring (C â†’â‚—[R] A) where
  left_distrib f g h := by ext; simp [map_add_right]
  right_distrib f g h := by ext; simp [map_add_left]
  zero_mul f := by ext; simp
  mul_zero f := by ext; simp

scoped[ConvolutionProduct] attribute [instance] LinearMap.convNonUnitalNonAssocSemiring

@[simp] lemma toSpanSingleton_convMul_toSpanSingleton (x y : A) :
    toSpanSingleton R A x * toSpanSingleton R A y = toSpanSingleton R A (x * y) := by ext; simp

end NonUnitalNonAssocSemiring

open scoped ConvolutionProduct

section NonUnitalNonAssocRing
variable [NonUnitalNonAssocRing A] [Module R A] [SMulCommClass R A A] [IsScalarTower R A A]
  [AddCommMonoid C] [Module R C] [CoalgebraStruct R C]

/-- Non-unital and non-associative convolution ring structure on linear maps from a
coalgebra to a non-unital and non-associative algebra. -/
abbrev convNonUnitalNonAssocRing : NonUnitalNonAssocRing (C â†’â‚—[R] A) where

scoped[ConvolutionProduct] attribute [instance] LinearMap.convNonUnitalNonAssocRing

end NonUnitalNonAssocRing

section NonUnitalSemiring
variable [NonUnitalSemiring A] [Module R A] [SMulCommClass R A A] [IsScalarTower R A A]
  [AddCommMonoid C] [Module R C] [Coalgebra R C]

lemma convMul_comp_coalgHom_distrib [AddCommMonoid B] [Module R B] [CoalgebraStruct R B]
    (f g : C â†’â‚—[R] A) (h : B â†’â‚—c[R] C) :
    (f * g).comp h.toLinearMap = f.comp h.toLinearMap * g.comp h.toLinearMap := by
  simp [convMul_def, map_comp, comp_assoc]

/-- Non-unital convolution semiring structure on linear maps from a coalgebra to a
non-unital algebra. -/
abbrev convNonUnitalSemiring : NonUnitalSemiring (C â†’â‚—[R] A) where
  mul_assoc f g h := calc
        Î¼ âˆ˜â‚— (Î¼ âˆ˜â‚— (f âŠ—â‚˜ g) âˆ˜â‚— Î´ âŠ—â‚˜ h) âˆ˜â‚— Î´
    _ = (Î¼ âˆ˜â‚— .rTensor _ Î¼) âˆ˜â‚— ((f âŠ—â‚˜ g) âŠ—â‚˜ h) âˆ˜â‚— (.rTensor _ Î´ âˆ˜â‚— Î´) := by
      rw [comp_assoc, â† comp_assoc _ _ (rTensor _ _), rTensor_comp_map,
        â† comp_assoc _ (rTensor _ _), map_comp_rTensor, comp_assoc]
    _ = (Î¼ âˆ˜â‚— rTensor _ Î¼)
        âˆ˜â‚— (((f âŠ—â‚˜ g) âŠ—â‚˜ h) âˆ˜â‚— (TensorProduct.assoc R C C C).symm) âˆ˜â‚— lTensor C Î´ âˆ˜â‚— Î´ := by
      simp only [comp_assoc, coassoc_symm]
    _ = (Î¼ âˆ˜â‚— rTensor A Î¼ âˆ˜â‚— â†‘(TensorProduct.assoc R A A A).symm)
        âˆ˜â‚— (f âŠ—â‚˜ (g âŠ—â‚˜ h)) âˆ˜â‚— lTensor C Î´ âˆ˜â‚— Î´ := by
      simp only [map_map_comp_assoc_symm_eq, comp_assoc]
    _ = (Î¼ âˆ˜â‚— .lTensor _ Î¼) âˆ˜â‚— (f âŠ—â‚˜ (g âŠ—â‚˜ h)) âˆ˜â‚— (lTensor C Î´ âˆ˜â‚— Î´) := by
      congr 1
      ext
      simp [mul_assoc]
    _ = Î¼ âˆ˜â‚— (f âŠ—â‚˜ Î¼ âˆ˜â‚— (g âŠ—â‚˜ h) âˆ˜â‚— Î´) âˆ˜â‚— Î´ := by
      rw [comp_assoc, â† comp_assoc _ _ (lTensor _ _), lTensor_comp_map,
        â† comp_assoc _ (lTensor _ _), map_comp_lTensor, comp_assoc]

scoped[ConvolutionProduct] attribute [instance] LinearMap.convNonUnitalSemiring

end NonUnitalSemiring

section NonUnitalRing
variable [NonUnitalRing A] [AddCommMonoid C] [Module R A] [SMulCommClass R A A]
  [IsScalarTower R A A] [Module R C] [Coalgebra R C]

/-- Non-unital convolution ring structure on linear maps from a coalgebra to a
non-unital algebra. -/
abbrev convNonUnitalRing : NonUnitalRing (C â†’â‚—[R] A) where

scoped[ConvolutionProduct] attribute [instance] LinearMap.convNonUnitalRing

end NonUnitalRing

section Semiring
variable [Semiring A] [Algebra R A] [Semiring B] [Algebra R B] [AddCommMonoid C] [Module R C]

section CoalgebraStruct
variable [CoalgebraStruct R C]

lemma algHom_comp_convMul_distrib (h : A â†’â‚ B) (f g : C â†’â‚—[R] A) :
    h.toLinearMap.comp (f * g) = h.toLinearMap.comp f * h.toLinearMap.comp g := by
  simp [convMul_def, map_comp, â† comp_assoc, AlgHom.comp_mul']

end CoalgebraStruct

variable [Coalgebra R C]

/-- Convolution unit on linear maps from a coalgebra to an algebra. -/
abbrev convOne : One (C â†’â‚—[R] A) where one := Algebra.linearMap R A âˆ˜â‚— counit

scoped[ConvolutionProduct] attribute [instance] LinearMap.convOne LinearMap.convMul

lemma convOne_def : (1 : C â†’â‚—[R] A) = Algebra.linearMap R A âˆ˜â‚— counit := rfl

@[simp] lemma convOne_apply (c : C) : (1 : C â†’â‚—[R] A) c = algebraMap R A (counit c) := rfl

/-- Convolution semiring structure on linear maps from a coalgebra to an algebra. -/
abbrev convSemiring : Semiring (C â†’â‚—[R] A) where
  one_mul f := by ext; simp [convOne_def, â† map_comp_rTensor]
  mul_one f := by ext; simp [convOne_def, â† map_comp_lTensor]

scoped[ConvolutionProduct] attribute [instance] LinearMap.convSemiring

end Semiring

section CommSemiring
variable [CommSemiring A] [AddCommMonoid C] [Algebra R A] [Module R C] [Coalgebra R C]
  [IsCocomm R C]

/-- Commutative convolution semiring structure on linear maps from a cocommutative coalgebra to an
algebra. -/
abbrev convCommSemiring : CommSemiring (C â†’â‚—[R] A) where
  mul_comm f g := by
    rw [convMul_def, â† comm_comp_comul, â† LinearMap.comp_assoc Î´, map_comp_comm_eq, convMul_def,
      â† LinearMap.comp_assoc, â† LinearMap.comp_assoc, â† LinearMap.comp_assoc]
    congr 3
    ext; exact mul_comm _ _

scoped[ConvolutionProduct] attribute [instance] LinearMap.convCommSemiring

end CommSemiring

section Ring
variable [Ring A] [AddCommMonoid C] [Algebra R A] [Module R C] [Coalgebra R C]

/-- Convolution ring structure on linear maps from a coalgebra to an algebra. -/
abbrev convRing : Ring (C â†’â‚—[R] A) where

scoped[ConvolutionProduct] attribute [instance] LinearMap.convRing

end Ring

section CommRing
variable [CommRing A] [AddCommMonoid C] [Algebra R A] [Module R C] [Coalgebra R C] [IsCocomm R C]

/-- Commutative convolution ring structure on linear maps from a cocommutative coalgebra to an
algebra. -/
abbrev convCommRing : CommRing (C â†’â‚—[R] A) where

scoped[ConvolutionProduct] attribute [instance] LinearMap.convCommRing

end CommRing
end LinearMap
