import Mathlib.Analysis.Convex.Exposed
import Mathlib.Analysis.Convex.Extreme
import Mathlib.Analysis.Convex.Cone.Pointed
import Toric.Mathlib.Analysis.Convex.Extreme
import Toric.Mathlib.Analysis.Convex.Hull

/-!
# TODO

Make cone explicit in `ConvexCone.toPointedCone`
-/

open scoped InnerProductSpace

namespace PointedCone
variable {ğ•œ E : Type*}

section Module
variable [Semiring ğ•œ] [PartialOrder ğ•œ] [IsOrderedRing ğ•œ] [AddCommMonoid E] [Module ğ•œ E]
  {S : Set E}

@[simp]
lemma _root_.ConvexCone.toPointedCone_top : (âŠ¤ : ConvexCone ğ•œ E).toPointedCone trivial = âŠ¤ := rfl

variable (ğ•œ S) in
/-- The span of a set `S` is the smallest pointed cone that contains `S`.

Pointed cones being defined as submodules over nonnegative scalars, this is exactly the
submodule span of `S` w.r.t. nonnegative scalars. -/
abbrev span : PointedCone ğ•œ E := Submodule.span _ S

lemma subset_span : S âŠ† PointedCone.span ğ•œ S := Submodule.subset_span

end Module

section NormedAddCommGroup
variable [NormedAddCommGroup E] [InnerProductSpace â„ E] {w x y : E} {s t : Set E}

-- TODO: Replace `dual`
variable (s) in
/-- The dual of an arbitrary set as a `PointedCone`. -/
def dual' : PointedCone â„ E := s.innerDualCone.toPointedCone <| pointed_innerDualCone s

@[simp, norm_cast]
lemma toConvexCone_dual' (s : Set E) : â†‘(dual' s) = (s : Set E).innerDualCone := rfl

lemma dual_eq_dual' (Ïƒ : PointedCone â„ E) : Ïƒ.dual = dual' (Ïƒ : Set E) := rfl

@[simp] lemma mem_dual' : y âˆˆ dual' s â†” âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ 0 â‰¤ âŸªx, yâŸ«_â„ := .rfl

@[gcongr] lemma dual'_le_dual' (h : s âŠ† t) : dual' t â‰¤ dual' s := fun _ hx y hy => hx y (h hy)

@[simp] lemma dual'_empty : dual' (âˆ… : Set E) = âŠ¤ := by simp [dual']

lemma dual'_union (s t : Set E) : dual' (s âˆª t) = dual' s âŠ“ dual' t := by
  refine le_antisymm (le_inf (dual'_le_dual' le_sup_left) (dual'_le_dual' le_sup_right)) ?_
  rintro x âŸ¨hxs, hxtâŸ© y (hy | hy)
  exacts [hxs y hy, hxt y hy]

lemma dual_span (s : Set E) : (span â„ s).dual = dual' s := by
  refine le_antisymm (innerDualCone_le_innerDualCone subset_span) ?_
  intro x hx
  apply Submodule.span_induction
  Â· intro y hy
    exact hx y hy
  Â· simp only [inner_zero_left, le_refl]
  Â· intro y z _ _ hxy hyz
    rw [inner_add_left]
    exact add_nonneg hxy hyz
  Â· intro t y _ hxy
    erw [inner_smul_real_left]
    exact mul_nonneg t.prop hxy

lemma inner_nonneg_of_mem_span_inner_nonneg (hs : âˆ€ x âˆˆ s, 0 â‰¤ âŸªx, wâŸ«_â„) (hx : x âˆˆ span â„ s) :
    0 â‰¤ âŸªx, wâŸ«_â„ := by
  induction hx using Submodule.span_induction with
  | mem x hx => exact hs _ hx
  | zero => simp
  | add x y hx hy hxw hyw =>
    rw [inner_add_left]
    exact add_nonneg hxw hyw
  | smul t x hx hxw =>
    rw [Nonneg.mk_smul, real_inner_smul_left]
    exact mul_nonneg t.2 hxw

lemma inner_nonpos_of_mem_span_inner_nonpos (hs : âˆ€ x âˆˆ s, âŸªx, wâŸ«_â„ â‰¤ 0) (hx : x âˆˆ span â„ s) :
    âŸªx, wâŸ«_â„ â‰¤ 0 := by
  induction hx using Submodule.span_induction with
  | mem x hx => exact hs _ hx
  | zero => simp
  | add x y hx hy hxw hyw =>
    rw [inner_add_left]
    exact add_nonpos hxw hyw
  | smul t x hx hxw =>
    rw [Nonneg.mk_smul, real_inner_smul_left]
    exact mul_nonpos_of_nonneg_of_nonpos t.2 hxw

lemma eq_zero_of_inner_eq_zero_of_mem_span_inner_pos (hs : âˆ€ x âˆˆ s, 0 < âŸªx, wâŸ«_â„)
    (hx : x âˆˆ span â„ s) (hw : âŸªx, wâŸ«_â„ = 0) : x = 0 := by
  induction hx using Submodule.span_induction with
  | mem x h => exact False.elim <| ne_of_lt (hs x h) hw.symm
  | zero => rfl
  | add x y hx hy hxw hyw =>
    rw [inner_add_left] at hw
    have H : âŸªx, wâŸ«_â„ = 0 âˆ§ âŸªy,wâŸ«_â„ = 0 := (add_eq_zero_iff_of_nonneg
      (inner_nonneg_of_mem_span_inner_nonneg (fun z hz => (hs z hz).le) hx)
      (inner_nonneg_of_mem_span_inner_nonneg (fun z hz => (hs z hz).le) hy)).mp hw
    rw [hxw H.1, hyw H.2]
    exact zero_add 0
  | smul t x hx hxw =>
    rw [smul_eq_zero]
    rw [Nonneg.mk_smul, real_inner_smul_left, mul_eq_zero, NNReal.val_eq_coe,
      NNReal.coe_eq_zero] at hw
    exact Or.elim hw Or.inl (fun h => Or.inr (hxw h))

lemma eq_zero_of_inner_eq_zero_of_mem_span_inner_neg (hs : âˆ€ x âˆˆ s, âŸªx, wâŸ«_â„ < 0)
    (hx : x âˆˆ span â„ s) (hw : âŸªx, wâŸ«_â„ = 0) : x = 0 := by
  induction hx using Submodule.span_induction with
  | mem x h => exact False.elim <| ne_of_lt (hs x h) hw
  | zero => rfl
  | add x y hx hy hxw hyw =>
    rw [inner_add_left] at hw
    have H := (add_eq_zero_iff_of_nonneg
      (neg_nonneg_of_nonpos <|
        inner_nonpos_of_mem_span_inner_nonpos (fun z hz => (hs z hz).le) hx)
      (neg_nonneg_of_nonpos <|
        inner_nonpos_of_mem_span_inner_nonpos (fun z hz => (hs z hz).le) hy)).mp <| by
      rw [â†neg_add, hw, neg_zero]
    rw [neg_eq_zero, neg_eq_zero] at H
    rw [hxw H.1, hyw H.2]
    exact zero_add 0
  | smul t x hx hxw =>
    rw [smul_eq_zero]
    rw [Nonneg.mk_smul, real_inner_smul_left, mul_eq_zero, NNReal.val_eq_coe,
      NNReal.coe_eq_zero] at hw
    exact Or.elim hw .inl fun h => .inr (hxw h)

end NormedAddCommGroup

section LinearOrderedField

variable {ğ•œ E : Type*} [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
  [AddCommGroup E] [Module ğ•œ E]

theorem coe_span_eq_convexHull_of_smul_mem {s : Set E} (hn : s.Nonempty)
    (h : âˆ€ x âˆˆ s, âˆ€ a : ğ•œ, a â‰¥ 0 â†’ a â€¢ x âˆˆ s) :
    span ğ•œ s = convexHull ğ•œ s := by
  obtain âŸ¨x, hxâŸ© := hn
  apply subset_antisymm
  Â· apply Submodule.span_induction
    Â· exact subset_convexHull ğ•œ s
    Â· apply subset_convexHull ğ•œ s
      simpa using h x hx 0
    Â· intro y z _ _ hy hz
      have hyâ‚‚ := smul_mem_convexHull h y hy 2 (by positivity)
      have hzâ‚‚ := smul_mem_convexHull h z hz 2 (by positivity)
      simpa using convex_convexHull ğ•œ s hyâ‚‚ hzâ‚‚ (a := 1 / 2) (b := 1 / 2)
        (by positivity) (by positivity) (by ring)
    Â· intro a x _ hx
      exact smul_mem_convexHull h x hx a.1 a.2
  Â· intro y hy
    apply mem_convexHull_iff.mp hy
    Â· exact subset_span
    Â· exact (span ğ•œ s).toConvexCone.convex

theorem coe_span_eq_convexHull {s : Set E} (hn : s.Nonempty) :
    span ğ•œ s = convexHull ğ•œ { x | âˆƒ (r : { r : ğ•œ // 0 â‰¤ r }) (y : E), y âˆˆ s âˆ§ x = r â€¢ y } := by
  let t := { x | âˆƒ (r : { r : ğ•œ // 0 â‰¤ r }) (y : E), y âˆˆ s âˆ§ x = r â€¢ y }
  rw [â† coe_span_eq_convexHull_of_smul_mem]
  Â· simp only [SetLike.coe_set_eq]
    apply le_antisymm
    Â· rw [Submodule.span_le]
      intro x hx
      apply subset_span
      exact âŸ¨âŸ¨1, by positivityâŸ©, x, hx, by simpâŸ©
    Â· rw [Submodule.span_le]
      rintro x âŸ¨r, x, hx, rflâŸ©
      apply Submodule.smul_mem
      apply subset_span hx
  Â· obtain âŸ¨x, hxâŸ© := hn
    exact âŸ¨x, 1, x, hx, by simpâŸ©
  Â· simp only [Subtype.exists, Nonneg.mk_smul]
    rintro x âŸ¨r, hr, y, hy, rflâŸ©
    intro r' hr'
    rw [smul_smul]
    refine âŸ¨r' * r, by positivity, y, hy, rflâŸ©

theorem mem_span_iff_mem_convexHull {s : Set E} (hn : s.Nonempty) {x : E} :
    x âˆˆ span ğ•œ s â†”
      x âˆˆ convexHull ğ•œ { x | âˆƒ (r : { r : ğ•œ // 0 â‰¤ r }) (y : E), y âˆˆ s âˆ§ x = r â€¢ y } := by
  rw [â† coe_span_eq_convexHull hn]
  rfl

private theorem smul_mem_of_isExtreme.aux {a b c : ğ•œ} {x : E} (hab : a < b) (hbc : b < c) :
    b â€¢ x âˆˆ openSegment ğ•œ (a â€¢ x) (c â€¢ x) := by
  have hba : b - a > 0 := by simp_all
  have hcb : c - b > 0 := by simp_all
  have hca : c - a > 0 := by simpa using add_pos hba hcb
  refine âŸ¨(c - b) / (c - a), (b - a) / (c - a), by positivity, by positivity, ?_, ?_âŸ©
  Â· rw [â† add_div, sub_add_sub_cancel, div_self]
    positivity
  Â· simp only [smul_smul, smul_smul, â† add_smul, mul_comm, mul_div, â† add_div]
    congr
    rw [div_eq_iff (by positivity)]
    ring

theorem smul_mem_of_isExtreme {c : PointedCone ğ•œ E} {s : Set E} (he : IsExtreme ğ•œ c s) :
    âˆ€ x âˆˆ s, âˆ€ a : ğ•œ, a â‰¥ 0 â†’ a â€¢ x âˆˆ s := by
  intro x hxs a ha
  by_cases hcmp : a â‰¤ 1
  Â· by_cases a = 1 <;> (try simp_all; done)
    refine he.2 (c.smul_mem âŸ¨a, haâŸ© (he.1 hxs))
      (c.smul_mem âŸ¨2, by positivityâŸ© (he.1 hxs)) hxs ?_ |>.1
    simpa using smul_mem_of_isExtreme.aux (ğ•œ := ğ•œ) (E := E) (b := 1)
      (lt_of_le_of_ne hcmp â€¹_â€º) (by simp)
  Â· refine he.2 (c.smul_mem âŸ¨0, le_rflâŸ© (he.1 hxs)) (c.smul_mem âŸ¨a, haâŸ© (he.1 hxs)) hxs ?_ |>.2
    simpa using smul_mem_of_isExtreme.aux (ğ•œ := ğ•œ) (E := E) (a := 0) (b := 1)
      (by simp) (by simp_all)

theorem mem_span_inter_of_mem_span_of_isExtreme (c : PointedCone ğ•œ E)
    {t : Set E} (h : âˆ€ r : { r : ğ•œ // 0 â‰¤ r }, âˆ€ x âˆˆ t, r â€¢ x âˆˆ t) (he : IsExtreme ğ•œ c t) :
    t âŠ† c âˆ§ âˆ€ g : Set E, g âŠ† c â†’ âˆ€ x âˆˆ span ğ•œ g, x âˆˆ t â†’ x âˆˆ span ğ•œ (g âˆ© t) := by
  have := smul_mem_of_isExtreme he
  simp only [isExtreme_iff_mem_convexHull_inter_of_mem_convexHull c t c.toConvexCone.convex] at he
  refine âŸ¨he.1, ?_âŸ©
  intro g hgc x hxg hxt
  by_cases hg : g.Nonempty
  Â· rw [mem_span_iff_mem_convexHull hg] at hxg
    let g' := { x : E | âˆƒ r : { r : ğ•œ // 0 â‰¤ r }, âˆƒ y âˆˆ g, x = r â€¢ y }
    replace he' := he.2 g' ?_ x hxg hxt
    Â· have : g' âˆ© t âŠ† { x : E | x = 0 âˆ¨ âˆƒ r : { r : ğ•œ // 0 â‰¤ r }, âˆƒ y âˆˆ g âˆ© t, x = r â€¢ y } := by
        rintro _ âŸ¨âŸ¨r, y, hyg, rflâŸ©, hxtâŸ©
        by_cases hr : r = 0
        Â· simp_all
        Â· refine Or.inr âŸ¨r, y, âŸ¨hyg, ?_âŸ©, rflâŸ©
          specialize h (1 / r) _ hxt
          obtain âŸ¨r, hrâŸ© := r
          simp only [one_div, Nonneg.inv_mk, Nonneg.mk_smul] at h
          rwa [inv_smul_smulâ‚€] at h
          intro h
          apply hr
          ext
          exact h
      replace he' := convexHull_mono this he'
      by_cases h' : (g âˆ© t).Nonempty
      Â· rw [mem_span_iff_mem_convexHull h']
        convert he'
        simp only [Set.mem_inter_iff, Subtype.exists, Nonneg.mk_smul, exists_prop, iff_or_self]
        intro h
        obtain âŸ¨y, âŸ¨hyg, hytâŸ©âŸ© := h'
        exact âŸ¨0, le_rfl, y, âŸ¨hyg, hytâŸ©, by simp [h]âŸ©
      Â· simp only [Set.not_nonempty_iff_eq_empty] at h'
        simpa [h'] using he'
    Â· rintro _ âŸ¨r, y, hyg, rflâŸ©
      apply Submodule.smul_mem
      exact hgc hyg
  Â· simp_all [Set.not_nonempty_iff_eq_empty]

theorem span_eq_of_isExtreme_of_convex {c : PointedCone ğ•œ E} {s : Set E} (hn : s.Nonempty)
    (he : IsExtreme ğ•œ c s) (hc : Convex ğ•œ s) :
    span ğ•œ s = s := by
  apply le_antisymm
  Â· apply Submodule.span_induction
    Â· exact fun _ h => h
    Â· obtain âŸ¨x, hxâŸ© := hn
      simpa using smul_mem_of_isExtreme he x hx 0
    Â· intro x y hx hy hxs hys
      have hxâ‚‚ : (2 : ğ•œ) â€¢ x âˆˆ s := smul_mem_of_isExtreme he x hxs (a := 2) (by positivity)
      have hyâ‚‚ : (2 : ğ•œ) â€¢ y âˆˆ s := smul_mem_of_isExtreme he y hys (a := 2) (by positivity)
      have := hc hxâ‚‚ hyâ‚‚ (a := 1 / 2) (b := 1 / 2) (by positivity) (by positivity) (by ring)
      simpa [smul_smul] using this
    Â· intro a y _ hys
      exact smul_mem_of_isExtreme he y hys a.1 a.2
  Â· exact Submodule.subset_span

theorem span_eq_of_isExposed [TopologicalSpace ğ•œ] [TopologicalSpace E] {c : PointedCone ğ•œ E}
    {s : Set E} (hn : s.Nonempty) (he : IsExposed ğ•œ c s) :
    span ğ•œ s = s :=
  c.span_eq_of_isExtreme_of_convex hn he.isExtreme (he.convex c.toConvexCone.convex)

end LinearOrderedField
end PointedCone
