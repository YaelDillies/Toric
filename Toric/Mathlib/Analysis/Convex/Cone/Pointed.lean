import Mathlib.Analysis.Convex.Cone.Pointed

open scoped InnerProductSpace

namespace PointedCone
variable {ùïú E : Type*}

section Module
variable [OrderedSemiring ùïú] [AddCommMonoid E] [Module ùïú E] {S : Set E}

variable (ùïú S) in
/-- The span of a set `S` is the smallest pointed cone that contains `S`.

Pointed cones being defined as submodules over nonnegative scalars, this is exactly the
submodule span of `S` w.r.t. nonnegative scalars. -/
abbrev span : PointedCone ùïú E := Submodule.span _ S

lemma subset_span : S ‚äÜ PointedCone.span ùïú S := Submodule.subset_span

end Module

section NormedAddCommGroup
variable [NormedAddCommGroup E] [InnerProductSpace ‚Ñù E] {w x : E} {S : Set E}

lemma inner_nonneg_of_mem_span_inner_nonneg (hS : ‚àÄ x ‚àà S, 0 ‚â§ ‚ü™x, w‚ü´_‚Ñù) (hx : x ‚àà span ‚Ñù S) :
    0 ‚â§ ‚ü™x, w‚ü´_‚Ñù := by
  induction hx using Submodule.span_induction with
  | mem x hx => exact hS _ hx
  | zero => simp
  | add x y hx hy hxw hyw =>
    rw [inner_add_left]
    exact add_nonneg hxw hyw
  | smul t x hx hxw =>
    rw [Nonneg.mk_smul, real_inner_smul_left]
    exact mul_nonneg t.2 hxw

lemma inner_nonpos_of_mem_span_inner_nonpos (hS : ‚àÄ x ‚àà S, ‚ü™x, w‚ü´_‚Ñù ‚â§ 0) (hx : x ‚àà span ‚Ñù S) :
    ‚ü™x, w‚ü´_‚Ñù ‚â§ 0 := by
  induction hx using Submodule.span_induction with
  | mem x hx => exact hS _ hx
  | zero => simp
  | add x y hx hy hxw hyw =>
    rw [inner_add_left]
    exact add_nonpos hxw hyw
  | smul t x hx hxw =>
    rw [Nonneg.mk_smul, real_inner_smul_left]
    exact mul_nonpos_of_nonneg_of_nonpos t.2 hxw

lemma eq_zero_of_inner_eq_zero_of_mem_span_inner_pos
    (hS : ‚àÄ x ‚àà S, 0 < ‚ü™x, w‚ü´_‚Ñù) (h : x ‚àà span ‚Ñù S) (hw : ‚ü™x, w‚ü´_‚Ñù = 0) : x = 0 := by
  induction h using Submodule.span_induction with
  | mem x h => exact False.elim <| ne_of_lt (hS x h) hw.symm
  | zero => rfl
  | add x y hx hy hxw hyw =>
    rw [inner_add_left] at hw
    have H : ‚ü™x, w‚ü´_‚Ñù = 0 ‚àß ‚ü™y,w‚ü´_‚Ñù = 0 := (add_eq_zero_iff_of_nonneg
      (inner_nonneg_of_mem_span_inner_nonneg (fun z hz => (hS z hz).le) hx)
      (inner_nonneg_of_mem_span_inner_nonneg (fun z hz => (hS z hz).le) hy)).mp hw
    rw [hxw H.1, hyw H.2]
    exact zero_add 0
  | smul t x hx hxw =>
    rw [smul_eq_zero]
    rw [Nonneg.mk_smul, real_inner_smul_left, mul_eq_zero, NNReal.val_eq_coe,
      NNReal.coe_eq_zero] at hw
    exact Or.elim hw Or.inl (fun h => Or.inr (hxw h))

lemma eq_zero_of_inner_eq_zero_of_mem_span_inner_neg (w : E) (S : Set E)
    (hS : ‚àÄ x ‚àà S, ‚ü™x, w‚ü´_‚Ñù < 0) {x : E} (h : x ‚àà span ‚Ñù S) (hw : ‚ü™x, w‚ü´_‚Ñù = 0) : x = 0 := by
  induction h using Submodule.span_induction with
  | mem x h => exact False.elim <| ne_of_lt (hS x h) hw
  | zero => rfl
  | add x y hx hy hxw hyw =>
    rw [inner_add_left] at hw
    have H := (add_eq_zero_iff_of_nonneg
      (neg_nonneg_of_nonpos <|
        inner_nonpos_of_mem_span_inner_nonpos (fun z hz => (hS z hz).le) hx)
      (neg_nonneg_of_nonpos <|
        inner_nonpos_of_mem_span_inner_nonpos (fun z hz => (hS z hz).le) hy)).mp <| by
      rw [‚Üêneg_add, hw, neg_zero]
    rw [neg_eq_zero, neg_eq_zero] at H
    rw [hxw H.1, hyw H.2]
    exact zero_add 0
  | smul t x hx hxw =>
    rw [smul_eq_zero]
    rw [Nonneg.mk_smul, real_inner_smul_left, mul_eq_zero, NNReal.val_eq_coe,
      NNReal.coe_eq_zero] at hw
    exact Or.elim hw Or.inl (fun h => Or.inr (hxw h))

end NormedAddCommGroup
end PointedCone
