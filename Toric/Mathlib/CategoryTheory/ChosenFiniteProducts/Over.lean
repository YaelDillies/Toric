/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.CategoryTheory.ChosenFiniteProducts

/-!
# This is https://github.com/leanprover-community/mathlib4/pull/21399
-/

namespace CategoryTheory.Over

open Limits

variable {C : Type*} [Category C] [HasPullbacks C]

/-- A choice of finite products of `Over X` given by `Limits.pullback`. -/
@[reducible]
noncomputable
def chosenFiniteProducts (X : C) : ChosenFiniteProducts (Over X) where
  product Y Z := {
    cone := BinaryFan.mk (P := Over.mk (pullback.snd Y.hom Z.hom â‰« Z.hom))
      (Over.homMk (pullback.fst Y.hom Z.hom) pullback.condition)
      (Over.homMk (pullback.snd Y.hom Z.hom) rfl)
    isLimit := BinaryFan.isLimitMk
      (fun s â†¦ Over.homMk (pullback.lift s.fst.left s.snd.left <| by simp) <| by simp)
      (fun s â†¦ Over.OverMorphism.ext (pullback.lift_fst _ _ _))
      (fun s â†¦ Over.OverMorphism.ext (pullback.lift_snd _ _ _)) fun s m eâ‚ eâ‚‚ â†¦ by
      ext1
      apply pullback.hom_ext
      Â· simpa using congr(($eâ‚).left)
      Â· simpa using congr(($eâ‚‚).left)
  }
  terminal := âŸ¨asEmptyCone (Over.mk (ðŸ™ X)), IsTerminal.ofUniqueHom (fun Y â†¦ Over.homMk Y.hom)
    fun Y m â†¦ Over.OverMorphism.ext (by simpa using m.w)âŸ©

attribute [local instance] chosenFiniteProducts

open MonoidalCategory

variable {X : C}

@[ext]
lemma tensorObj_ext {R : C} {S T : Over X} (fâ‚ fâ‚‚ : R âŸ¶ (S âŠ— T).left)
    (eâ‚ : fâ‚ â‰« pullback.fst _ _ = fâ‚‚ â‰« pullback.fst _ _)
    (eâ‚‚ : fâ‚ â‰« pullback.snd _ _ = fâ‚‚ â‰« pullback.snd _ _) : fâ‚ = fâ‚‚ :=
  pullback.hom_ext eâ‚ eâ‚‚

@[simp]
lemma tensorObj_left (R S : Over X) : (R âŠ— S).left = Limits.pullback R.hom S.hom := rfl

@[simp]
lemma tensorObj_hom (R S : Over X) : (R âŠ— S).hom = pullback.snd R.hom S.hom â‰« S.hom := rfl

@[simp]
lemma tensorUnit_left : (ðŸ™_ (Over X)).left = X := rfl

@[simp]
lemma tensorUnit_hom : (ðŸ™_ (Over X)).hom = ðŸ™ X := rfl

@[reassoc (attr := simp)]
lemma associator_hom_left_fst (R S T : Over X) :
    (Î±_ R S T).hom.left â‰« pullback.fst R.hom (pullback.snd _ _ â‰« T.hom) =
      pullback.fst _ _ â‰« pullback.fst _ _ :=
  pullback.lift_fst _ _ _

@[reassoc (attr := simp)]
lemma associator_hom_left_snd_fst (R S T : Over X) :
    (Î±_ R S T).hom.left â‰« pullback.snd _ (pullback.snd _ _ â‰« T.hom) â‰« pullback.fst _ _ =
      pullback.fst _ _ â‰« pullback.snd _ _ :=
  (pullback.lift_snd_assoc _ _ _ _).trans (pullback.lift_fst _ _ _)

@[reassoc (attr := simp)]
lemma associator_hom_left_snd_snd (R S T : Over X) :
    (Î±_ R S T).hom.left â‰« pullback.snd _ (pullback.snd _ _ â‰« T.hom) â‰« pullback.snd _ _ =
      pullback.snd _ _ :=
  (pullback.lift_snd_assoc _ _ _ _).trans (pullback.lift_snd _ _ _)

@[reassoc (attr := simp)]
lemma associator_inv_left_fst (R S T : Over X) :
    (Î±_ R S T).inv.left â‰« pullback.fst (pullback.snd _ _ â‰« _) _ â‰« pullback.fst _ _ =
    pullback.fst _ _ :=
  (pullback.lift_fst_assoc _ _ _ _).trans (pullback.lift_fst _ _ _)

@[reassoc (attr := simp)]
lemma associator_inv_left_fst_snd (R S T : Over X) :
    (Î±_ R S T).inv.left â‰« pullback.fst (pullback.snd _ _ â‰« _) _ â‰« pullback.snd _ _ =
      pullback.snd _ _ â‰« pullback.fst _ _ :=
  (pullback.lift_fst_assoc _ _ _ _).trans (pullback.lift_snd _ _ _)

@[reassoc (attr := simp)]
lemma associator_inv_left_snd (R S T : Over X) :
    (Î±_ R S T).inv.left â‰« pullback.snd (pullback.snd _ _ â‰« _) _ =
    pullback.snd _ _ â‰« pullback.snd _ _ :=
  pullback.lift_snd _ _ _

@[simp]
lemma leftUnitor_hom_left (Y : Over X) :
    (Î»_ Y).hom.left = pullback.snd _ _ := rfl

@[reassoc (attr := simp)]
lemma leftUnitor_inv_left_fst (Y : Over X) :
    (Î»_ Y).inv.left â‰« pullback.fst (ðŸ™ X) _ = Y.hom :=
  pullback.lift_fst _ _ _

@[reassoc (attr := simp)]
lemma leftUnitor_inv_left_snd (Y : Over X) :
    (Î»_ Y).inv.left â‰« pullback.snd (ðŸ™ X) _ = ðŸ™ Y.left :=
  pullback.lift_snd _ _ _

@[simp]
lemma rightUnitor_hom_left (Y : Over X) :
    (Ï_ Y).hom.left = pullback.fst _ (ðŸ™ X) := rfl

@[reassoc (attr := simp)]
lemma rightUnitor_inv_left_fst (Y : Over X) :
    (Ï_ Y).inv.left â‰« pullback.fst _ (ðŸ™ X) = ðŸ™ _ :=
  pullback.lift_fst _ _ _

@[reassoc (attr := simp)]
lemma rightUnitor_inv_left_snd (Y : Over X) :
    (Ï_ Y).inv.left â‰« pullback.snd _ (ðŸ™ X) = Y.hom :=
  pullback.lift_snd _ _ _

lemma whiskerLeft_left {R S T : Over X} (f : S âŸ¶ T) :
    (R â— f).left = pullback.map _ _ _ _ (ðŸ™ _) f.left (ðŸ™ _) (by simp) (by simp) := rfl

@[reassoc (attr := simp)]
lemma whiskerLeft_left_fst {R S T : Over X} (f : S âŸ¶ T) :
    (R â— f).left â‰« pullback.fst _ _ = pullback.fst _ _ :=
  (pullback.lift_fst _ _ _).trans (Category.comp_id _)

@[reassoc (attr := simp)]
lemma whiskerLeft_left_snd {R S T : Over X} (f : S âŸ¶ T) :
    (R â— f).left â‰« pullback.snd _ _ = pullback.snd _ _ â‰« f.left :=
  pullback.lift_snd _ _ _

lemma whiskerRight_left {R S T : Over X} (f : S âŸ¶ T) :
    (f â–· R).left = pullback.map _ _ _ _ f.left (ðŸ™ _) (ðŸ™ _) (by simp) (by simp) := rfl

@[reassoc (attr := simp)]
lemma whiskerRight_left_fst {R S T : Over X} (f : S âŸ¶ T) :
    (f â–· R).left â‰« pullback.fst _ _ = pullback.fst _ _ â‰« f.left :=
  pullback.lift_fst _ _ _

@[reassoc (attr := simp)]
lemma whiskerRight_left_snd {R S T : Over X} (f : S âŸ¶ T) :
    (f â–· R).left â‰« pullback.snd _ _ = pullback.snd _ _ :=
  (pullback.lift_snd _ _ _).trans (Category.comp_id _)

lemma tensorHom_left {R S T U : Over X} (f : R âŸ¶ S) (g : T âŸ¶ U) :
    (f âŠ— g).left = pullback.map _ _ _ _ f.left g.left (ðŸ™ _) (by simp) (by simp) := rfl

@[reassoc (attr := simp)]
lemma tensorHom_left_fst {R S T U : Over X} (f : R âŸ¶ S) (g : T âŸ¶ U) :
    (f âŠ— g).left â‰« pullback.fst _ _ = pullback.fst _ _ â‰« f.left :=
  pullback.lift_fst _ _ _

@[reassoc (attr := simp)]
lemma tensorHom_left_snd {R S T U : Over X} (f : R âŸ¶ S) (g : T âŸ¶ U) :
    (f âŠ— g).left â‰« pullback.snd _ _ = pullback.snd _ _ â‰« g.left :=
  pullback.lift_snd _ _ _

@[simp]
lemma braiding_hom_left {R S : Over X} :
    (Î²_ R S).hom.left = (pullbackSymmetry _ _).hom := rfl

@[simp]
lemma braiding_inv_left {R S : Over X} :
    (Î²_ R S).inv.left = (pullbackSymmetry _ _).hom := rfl

end CategoryTheory.Over

namespace CategoryTheory.Under

open Limits

variable {C : Type*} [Category C] [HasPushouts C]

/-- A choice of finite products of `(Under X)áµ’áµ–` given by `Limits.pushout`. -/
@[reducible]
noncomputable
def chosenFiniteProducts (X : C) : ChosenFiniteProducts (Under X)áµ’áµ– where
  product Y Z := {
    cone := BinaryFan.mk (P := .op <| Under.mk <| Z.unop.hom â‰« pushout.inr Y.unop.hom Z.unop.hom)
      (.op <| Under.homMk (pushout.inl Y.unop.hom Z.unop.hom) pushout.condition)
      (.op <| Under.homMk (pushout.inr Y.unop.hom Z.unop.hom))
    isLimit := BinaryFan.isLimitMk
      (fun s â†¦ .op <| Under.homMk
        (pushout.desc s.fst.unop.right s.snd.unop.right <| by simp) <| by simp)
      (fun s â†¦ Quiver.Hom.unop_inj <| Under.UnderMorphism.ext (pushout.inl_desc _ _ _))
      (fun s â†¦ Quiver.Hom.unop_inj <| Under.UnderMorphism.ext (pushout.inr_desc _ _ _))
        fun s m eâ‚ eâ‚‚ â†¦ by
      refine  Quiver.Hom.unop_inj ?_
      ext1
      apply pushout.hom_ext
      Â· simpa using congr(($eâ‚).unop.right)
      Â· simpa using congr(($eâ‚‚).unop.right)
  }
  terminal.isLimit := terminalOpOfInitial Under.mkIdInitial

instance (X : C) : PreservesFiniteProducts (Under.opToOverOp X) := sorry

end CategoryTheory.Under
