/-
Copyright (c) 2025 Ya√´l Dillies, Micha≈Ç Mruga≈Ça, Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies, Micha≈Ç Mruga≈Ça, Andrew Yang
-/
import Mathlib.CategoryTheory.Monoidal.Cartesian.Grp_
import Toric.Mathlib.CategoryTheory.Monoidal.Cartesian.Mon_
import Toric.Mathlib.CategoryTheory.Monoidal.Mon_

open CategoryTheory Limits Mon_Class MonoidalCategory CartesianMonoidalCategory Opposite
open scoped Hom

universe v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

namespace CategoryTheory.Functor
variable {C D : Type*} [Category C] [Category D] [CartesianMonoidalCategory C]
  [CartesianMonoidalCategory D] {G : C} [Grp_Class G] (F : C ‚•§ D) [F.Monoidal]

scoped[Obj] attribute [instance] CategoryTheory.Functor.mon_ClassObj

open scoped Obj

/-- The image of a group object under a monoidal functor is a group object. -/
noncomputable abbrev grp_ClassObj : Grp_Class (F.obj G) := (F.mapGrp.obj ‚ü®G‚ü©).grp

scoped[Obj] attribute [instance] CategoryTheory.Functor.grp_ClassObj

end CategoryTheory.Functor

section

open CartesianMonoidalCategory MonoidalCategory

variable {C : Type*} [Category C] [CartesianMonoidalCategory C] {G H : Grp_ C}

@[simp] lemma Grp_.homMk_hom' {G H : Grp_ C} (f : G ‚ü∂ H) :
    homMk (G := G.X) (H := H.X) f.hom = f := rfl

lemma Grp_Class.inv_eq_comp_inv {G H : C} (f : G ‚ü∂ H) [Grp_Class H] : f ‚â´ Œπ = f‚Åª¬π := rfl

lemma Grp_Class.mul_eq_comp_mul {G H : C} {f g : G ‚ü∂ H} [Grp_Class H] : f * g = lift f g ‚â´ Œº := rfl

attribute [local simp] mul_eq_mul Grp_Class.inv_eq_inv comp_mul comp_mul_assoc
  mul_comp mul_comp_assoc Grp_Class.comp_inv one_eq_one
  Grp_Class.div_comp Grp_Class.div_comp_assoc one_comp

@[reassoc (attr := simp)]
lemma Grp_Class.one_inv [BraidedCategory C] {G : C} [Grp_Class G] : Œ∑[G] ‚â´ Œπ = Œ∑ := by simp

attribute [local simp] mul_comm mul_div_mul_comm

namespace Grp_Class
variable [BraidedCategory C]

instance : Grp_Class (ùüô_ C) where
  inv := ùüô _
  left_inv := toUnit_unique _ _
  right_inv := toUnit_unique _ _

namespace tensorObj

@[simps inv]
instance {G H : C} [Grp_Class G] [Grp_Class H] : Grp_Class (G ‚äó H) where
  inv := Œπ ‚äó‚Çò Œπ
  left_inv := by
    have H : ((ùüô G)‚Åª¬π ‚äó‚Çò (ùüô H)‚Åª¬π) * ùüô (G ‚äó H) = 1 := by
      simp only [‚Üê id_tensorHom_id, ‚Üê mul_tensorHom_mul, inv_mul_cancel, one_tensorHom_one]
    simpa [mul_tensorHom_mul, comp_mul, ‚Üê tensor_comp, one_eq_one, one_tensorHom_one]
  right_inv := by
    have H : ùüô (G ‚äó H) * ((ùüô G)‚Åª¬π ‚äó‚Çò (ùüô H)‚Åª¬π) = 1 := by
      simp only [‚Üê id_tensorHom_id, ‚Üê mul_tensorHom_mul, mul_inv_cancel, one_tensorHom_one]
    simpa [mul_tensorHom_mul, comp_mul, ‚Üê tensor_comp, one_eq_one, one_tensorHom_one]

end tensorObj
end Grp_Class

namespace Grp_

variable [BraidedCategory C] {G H H‚ÇÅ H‚ÇÇ : Grp_ C}

@[simps! tensorObj_X tensorHom_hom]
instance instMonoidalCategoryStruct : MonoidalCategoryStruct (Grp_ C) where
  tensorObj G H := ‚ü®G.X ‚äó H.X‚ü©
  tensorHom := tensorHom (C := Mon_ C)
  whiskerRight f G := whiskerRight (C := Mon_ C) f G.toMon_
  whiskerLeft G _ _ f := MonoidalCategoryStruct.whiskerLeft (C := Mon_ C) G.toMon_ f
  tensorUnit := ‚ü®ùüô_ C‚ü©
  associator X Y Z :=
    (Grp_.fullyFaithfulForget‚ÇÇMon_ C).preimageIso (associator X.toMon_ Y.toMon_ Z.toMon_)
  leftUnitor G := (Grp_.fullyFaithfulForget‚ÇÇMon_ C).preimageIso (leftUnitor G.toMon_)
  rightUnitor G := (Grp_.fullyFaithfulForget‚ÇÇMon_ C).preimageIso (rightUnitor G.toMon_)

@[simp] lemma tensorUnit_X : (ùüô_ (Grp_ C)).X = ùüô_ C := rfl

@[simp] lemma tensorUnit_one : Œ∑[(ùüô_ (Grp_ C)).X] = Œ∑[ùüô_ C] := rfl

@[simp] lemma tensorUnit_mul : Œº[(ùüô_ (Grp_ C)).X] = Œº[ùüô_ C] := rfl

@[simp] lemma tensorObj_one (G H : Grp_ C) : Œ∑[(G ‚äó H).X] = Œ∑[G.X ‚äó H.X] := rfl

@[simp] lemma tensorObj_mul (G H : Grp_ C) : Œº[(G ‚äó H).X] = Œº[G.X ‚äó H.X] := rfl

@[simp] lemma whiskerLeft_hom {G H : Grp_ C} (f : G ‚ü∂ H) (I : Grp_ C) :
    (f ‚ñ∑ I).hom = f.hom ‚ñ∑ I.X := rfl

@[simp] lemma whiskerRight_hom (G : Grp_ C) {H I : Grp_ C} (f : H ‚ü∂ I) :
    (G ‚óÅ f).hom = G.X ‚óÅ f.hom := rfl

@[simp] lemma leftUnitor_hom_hom (G : Grp_ C) : (Œª_ G).hom.hom = (Œª_ G.X).hom := rfl
@[simp] lemma leftUnitor_inv_hom (G : Grp_ C) : (Œª_ G).inv.hom = (Œª_ G.X).inv := rfl
@[simp] lemma rightUnitor_hom_hom (G : Grp_ C) : (œÅ_ G).hom.hom = (œÅ_ G.X).hom := rfl
@[simp] lemma rightUnitor_inv_hom (G : Grp_ C) : (œÅ_ G).inv.hom = (œÅ_ G.X).inv := rfl

@[simp] lemma associator_hom_hom (G H I : Grp_ C) : (Œ±_ G H I).hom.hom = (Œ±_ G.X H.X I.X).hom := rfl
@[simp] lemma associator_inv_hom (G H I : Grp_ C) : (Œ±_ G H I).inv.hom = (Œ±_ G.X H.X I.X).inv := rfl

instance instMonoidalCategory : MonoidalCategory (Grp_ C) where
  tensorHom_def := by intros; ext; simp [tensorHom_def]
  triangle _ _ := by ext; exact triangle _ _

instance instCartesianMonoidalCategory : CartesianMonoidalCategory (Grp_ C) where
  isTerminalTensorUnit :=
    .ofUniqueHom (fun G ‚Ü¶ toUnit G.toMon_) fun G f ‚Ü¶ by ext; exact toUnit_unique ..
  fst G H := fst G.toMon_ H.toMon_
  snd G H := snd G.toMon_ H.toMon_
  tensorProductIsBinaryProduct G H :=
    BinaryFan.IsLimit.mk _ (fun {T} f g ‚Ü¶ .mk (lift f.hom g.hom))
      (by aesop_cat) (by aesop_cat) (by aesop_cat)
  fst_def G H := Mon_.Hom.ext <| fst_def _ _
  snd_def G H := Mon_.Hom.ext <| snd_def _ _

@[simp] lemma lift_hom (f : G ‚ü∂ H‚ÇÅ) (g : G ‚ü∂ H‚ÇÇ) : (lift f g).hom = lift f.hom g.hom := rfl
@[simp] lemma fst_hom (G H : Grp_ C) : (fst G H).hom = fst G.X H.X := rfl
@[simp] lemma snd_hom (G H : Grp_ C) : (snd G H).hom = snd G.X H.X := rfl

@[simps]
instance : (forget‚ÇÇMon_ C).Monoidal where
  Œµ := ùüô _
  ¬´Œº¬ª G H := ùüô _
  ¬´Œ∑¬ª := ùüô _
  Œ¥ G H := ùüô _

instance instBraidedCategory : BraidedCategory (Grp_ C) :=
  .ofFaithful (forget‚ÇÇMon_ C) fun G H ‚Ü¶ Grp_.mkIso (Œ≤_ G.X H.X)

@[simp] lemma braiding_hom_hom (G H : Grp_ C) : (Œ≤_ G H).hom.hom = (Œ≤_ G.X H.X).hom := rfl
@[simp] lemma braiding_inv_hom (G H : Grp_ C) : (Œ≤_ G H).inv.hom = (Œ≤_ G.X H.X).inv := rfl

variable [IsCommMon H.X]

-- TODO: Make `Grp_.toMon_` an abbrev in mathlib.
set_option allowUnsafeReducibility true in
attribute [reducible] Grp_.toMon_

namespace Hom

instance : Mon_Class H where
  one := Œ∑[H.toMon_]
  mul := Œº[H.toMon_]
  one_mul := Mon_Class.one_mul H.toMon_
  mul_one := Mon_Class.mul_one H.toMon_
  mul_assoc := Mon_Class.mul_assoc H.toMon_

@[simp] lemma hom_one : (1 : G ‚ü∂ H).hom = 1 := rfl

@[simp] lemma hom_mul (f g : G ‚ü∂ H) : (f * g).hom = f.hom * g.hom := rfl

@[simp] lemma hom_pow (f : G ‚ü∂ H) (n : ‚Ñï) : (f ^ n).hom = f.hom ^ n := by
  induction n <;> simp [pow_succ, *]

/-- A commutative group object is a group object in the category of group objects. -/
instance : Grp_Class H where inv := .mk Œπ[H.X]

instance {f : G ‚ü∂ H} : IsMon_Hom f.hom‚Åª¬π where

@[simp] lemma hom_inv (f : G ‚ü∂ H) : f‚Åª¬π.hom = f.hom‚Åª¬π := rfl
@[simp] lemma hom_div (f g : G ‚ü∂ H) : (f / g).hom = f.hom / g.hom := rfl
@[simp] lemma hom_zpow (f : G ‚ü∂ H) (n : ‚Ñ§) : (f ^ n).hom = f.hom ^ n := by cases n <;> simp

end Hom

/-- A commutative group object is a commutative group object in the category of group objects. -/
instance : IsCommMon H where

end Grp_

variable {C : Type*} [Category C] [CartesianMonoidalCategory C] [BraidedCategory C] {G : C}

instance mk.X.instIsComm_Mon [Grp_Class G] [IsCommMon G] : IsCommMon (Grp_.mk G).X := ‚Äπ_‚Ä∫

end

instance {C : Type*} [Category C] [CartesianMonoidalCategory C] [BraidedCategory C]
    {G H : Grp_ C} [IsCommMon G.X] [IsCommMon H.X] (f : G ‚ü∂ H) : IsMon_Hom f where
  one_hom := by ext; simp [Grp_.Hom.instMon_Class_toric]
  mul_hom := by ext; simp [Grp_.Hom.instMon_Class_toric]

namespace CategoryTheory
variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] [CartesianMonoidalCategory C]
  {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] [CartesianMonoidalCategory D]
  {E : Type u‚ÇÉ} [Category.{v‚ÇÉ} E] [CartesianMonoidalCategory E]

namespace Functor
variable {F F' : C ‚•§ D} [F.Monoidal] [F'.Monoidal] {G : D ‚•§ E} [G.Monoidal]

open LaxMonoidal Monoidal

protected instance Faithful.mapGrp [F.Faithful] : F.mapGrp.Faithful where
  map_injective {_X _Y} _f _g hfg := F.mapMon.map_injective hfg

protected instance Full.mapGrp [F.Full] [F.Faithful] : F.mapGrp.Full where
  map_surjective := F.mapMon.map_surjective

/-- If `F : C ‚•§ D` is a fully faithful monoidal functor, then `Grp(F) : Grp C ‚•§ Grp D` is fully
faithful too. -/
protected noncomputable def FullyFaithful.mapGrp (hF : F.FullyFaithful) :
    F.mapGrp.FullyFaithful where
  preimage {X Y} f := Grp_.homMk <| hF.preimage f.hom

def _root_.Grp_Class.ofIso {X Y : C} (e : X ‚âÖ Y) [Grp_Class X] : Grp_Class Y where
  __ := Mon_Class.ofIso e
  inv := e.inv ‚â´ Œπ[X] ‚â´ e.hom
  left_inv := by simp [Mon_Class.ofIso]
  right_inv := by simp [Mon_Class.ofIso]

def FullyFaithful.grp_Class (hF : F.FullyFaithful) (X : C) [Grp_Class (F.obj X)] : Grp_Class X where
  __ := hF.mon_Class X
  inv := hF.preimage Œπ[F.obj X]
  left_inv := hF.map_injective
    (by simp [FullyFaithful.mon_Class, OplaxMonoidal.Œ∑_of_cartesianMonoidalCategory])
  right_inv := hF.map_injective
    (by simp [FullyFaithful.mon_Class, OplaxMonoidal.Œ∑_of_cartesianMonoidalCategory])

open EssImageSubcategory Monoidal in
/-- The essential image of a full and faithful functor between cartesian-monoidal categories is the
same on group objects as on objects. -/
@[simp] lemma essImage_mapGrp [F.Full] [F.Faithful] {G : Grp_ D} :
    F.mapGrp.essImage G ‚Üî F.essImage G.X where
  mp := by rintro ‚ü®H, ‚ü®e‚ü©‚ü©; exact ‚ü®H.X, ‚ü®(Grp_.forget _).mapIso e‚ü©‚ü©
  mpr := by
    rintro ‚ü®H, ‚ü®e‚ü©‚ü©
    letI h‚ÇÅ := Grp_Class.ofIso e.symm
    letI h‚ÇÇ := FullyFaithful.grp_Class (.ofFullyFaithful F) H
    refine ‚ü®‚ü®H‚ü©, ‚ü®Grp_.mkIso e ?_ ?_‚ü©‚ü© <;>
      simp [Grp_Class.ofIso, Mon_Class.ofIso, FullyFaithful.mon_Class, FullyFaithful.grp_Class,
        h‚ÇÅ, h‚ÇÇ]

variable [BraidedCategory C] [BraidedCategory D] (F : C ‚•§ D) [F.Braided]

noncomputable instance mapGrp.instMonoidal : F.mapGrp.Monoidal :=
  Functor.CoreMonoidal.toMonoidal
  { ŒµIso := (Grp_.fullyFaithfulForget‚ÇÇMon_ _).preimageIso (ŒµIso F.mapMon)
    ŒºIso X Y := (Grp_.fullyFaithfulForget‚ÇÇMon_ _).preimageIso (ŒºIso F.mapMon X.toMon_ Y.toMon_)
    ŒºIso_hom_natural_left f Z := by convert Œº_natural_left F.mapMon f Z.toMon_ using 1
    ŒºIso_hom_natural_right Z f := by convert Œº_natural_right F.mapMon Z.toMon_ f using 1
    associativity X Y Z := by convert associativity F.mapMon X.toMon_ Y.toMon_ Z.toMon_ using 1
    left_unitality X := by convert left_unitality F.mapMon X.toMon_ using 1
    right_unitality X := by convert right_unitality F.mapMon X.toMon_ using 1 }

end Functor
end CategoryTheory
