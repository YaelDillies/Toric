/-
Copyright (c) 2025 Ya√´l Dillies, Micha≈Ç Mruga≈Ça, Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies, Micha≈Ç Mruga≈Ça, Andrew Yang
-/
import Mathlib.CategoryTheory.Monoidal.Cartesian.Mon_
import Toric.Mathlib.CategoryTheory.Monoidal.Cartesian.Basic
import Toric.Mathlib.CategoryTheory.Monoidal.Functor
import Toric.Mathlib.CategoryTheory.Monoidal.CommMon_

open CategoryTheory Limits MonoidalCategory CartesianMonoidalCategory Mon_Class
open scoped Hom Obj

scoped[Hom] attribute [instance] Hom.monoid

universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

attribute [simp] Mon_Class.one_comp Mon_Class.one_comp_assoc Mon_Class.comp_one
  Mon_Class.comp_one_assoc

namespace CategoryTheory.Functor
variable {C D : Type*} [Category C] [Category D] [CartesianMonoidalCategory C]
  [CartesianMonoidalCategory D] {M X : C} [Mon_Class M] (F : C ‚•§ D) [F.Monoidal]

lemma map_mul (f g : X ‚ü∂ M) : F.map (f * g) = F.map f * F.map g := by
  simp only [Hom.mul_def, map_comp, obj.Œº_def, ‚Üê Category.assoc]
  congr 1
  rw [‚Üê IsIso.comp_inv_eq]
  ext <;> simp

@[simp]
lemma map_one : F.map (1 : X ‚ü∂ M) = 1 := by
  simp [Hom.one_def]

/-- `Functor.map` as a `MonoidHom`. -/
def mapMonoidHom : (X ‚ü∂ M) ‚Üí* (F.obj X ‚ü∂ F.obj M) where
  toFun := _
  map_one' := map_one F
  map_mul' := map_mul F

end CategoryTheory.Functor

variable {C : Type*} [Category C] [CartesianMonoidalCategory C] {M N X Y : C} [Mon_Class M]
  [Mon_Class N]

lemma Mon_.one_eq_one (M : Mon_ C) : M.one = 1 := Mon_Class.one_eq_one (M := M.X)

lemma Mon_.mul_eq_mul (M : Mon_ C) : M.mul = fst M.X M.X * snd M.X M.X :=
  Mon_Class.mul_eq_mul (M := M.X)

@[reassoc]
lemma Mon_Class.comp_pow (f : X ‚ü∂ M) (n : ‚Ñï) (h : Y ‚ü∂ X) : h ‚â´ f ^ n = (h ‚â´ f) ^ n := by
  induction n <;> simp [pow_succ, Mon_Class.comp_mul, *]

variable [BraidedCategory C]

lemma mul_tensor_mul (f f' : X ‚ü∂ M) (g g' : Y ‚ü∂ N) :
    (f * f') ‚äó (g * g') = (f ‚äó g) * (f' ‚äó g') := by
  simp [Hom.mul_def, Hom.one_def]

lemma one_tensor_one : (1 : X ‚ü∂ M) ‚äó (1 : Y ‚ü∂ N) = 1 := by
  simp only [Hom.one_def, tensor_comp, tensorObj.one_def, ‚Üê Category.assoc]
  congr 1
  rw [Iso.eq_comp_inv]
  exact toUnit_unique _ _

attribute [local simp]  one_eq_one

instance : IsMon_Hom (fst M N) where
instance : IsMon_Hom (snd M N) where

/-- If `M` is a commutative monoid object, then `Hom(X, M)` has a commutative monoid structure. -/
abbrev Hom.commMonoid [IsCommMon M] : CommMonoid (X ‚ü∂ M) where
  mul_comm f g := by
    show lift _ _ ‚â´ _ = lift _ _ ‚â´ _
    conv_lhs => rw [‚Üê IsCommMon.mul_comm]
    rw [‚Üê Category.assoc]
    congr 1
    ext <;> simp

scoped[Hom] attribute [instance] Hom.commMonoid

attribute [local simp] mul_eq_mul comp_mul mul_comp one_eq_one Mon_.one_eq_one Mon_.mul_eq_mul

namespace Mon_
variable {M N N‚ÇÅ N‚ÇÇ : Mon_ C}

instance instCartesianMonoidalCategory : CartesianMonoidalCategory (Mon_ C) where
  isTerminalTensorUnit :=
    .ofUniqueHom (fun M ‚Ü¶ .mk (toUnit _) (toUnit_unique ..))
      fun M f ‚Ü¶ by ext; exact toUnit_unique ..
  fst M N := .mk (fst M.X N.X)
  snd M N := .mk (snd M.X N.X)
  tensorProductIsBinaryProduct M N :=
    BinaryFan.IsLimit.mk _ (fun {T} f g ‚Ü¶ .mk (lift f.hom g.hom)) (by aesop_cat) (by aesop_cat)
      (by aesop_cat)
  fst_def M N := by ext; simp [fst_def]; congr
  snd_def M N := by ext; simp [snd_def]; congr

@[simp] lemma lift_hom (f : M ‚ü∂ N‚ÇÅ) (g : M ‚ü∂ N‚ÇÇ) : (lift f g).hom = lift f.hom g.hom := rfl
@[simp] lemma fst_hom (M N : Mon_ C) : (fst M N).hom = fst M.X N.X := rfl
@[simp] lemma snd_hom (M N : Mon_ C) : (snd M N).hom = snd M.X N.X := rfl

@[simp] lemma mul_comm [IsCommMon M.X] : (Œ≤_ _ _).hom ‚â´ M.mul = M.mul := IsCommMon.mul_comm _

/-- A commutative monoid object is a monoid object in the category of monoid objects. -/
instance [IsCommMon M.X] : Mon_Class M where
  one :=
    .mk M.one (by simp [Mon_.one_eq_one]) (by simp [toUnit_unique (œÅ_ (ùüô_ C)).hom (Œª_ (ùüô_ C)).hom])
  mul := .mk M.mul (by simp [toUnit_unique (œÅ_ (ùüô_ C)).hom (Œª_ (ùüô_ C)).hom]) <| by
    simp only [monMonoidalStruct_tensorObj_X, tensorObj_mul, tensorŒº, Category.assoc,
      tensorHom_def'_assoc, ‚Üê associator_inv_naturality_right_assoc,
      (Iso.inv_comp_eq _).mpr M.mul_assoc, ‚Üê MonoidalCategory.whiskerLeft_comp_assoc,
      ‚Üê M.mul_assoc, ‚Üê comp_whiskerRight_assoc]
    simp only [mul_comm, mul_assoc, Iso.inv_hom_id_assoc, MonoidalCategory.whiskerLeft_comp,
      Category.assoc, tensor_whiskerLeft]
  one_mul' := by ext; simp [leftUnitor_hom]
  mul_one' := by ext; simp [rightUnitor_hom]
  mul_assoc' := by ext; simp [_root_.mul_assoc]

@[simp] lemma hom_Œ∑ (M : Mon_ C) [IsCommMon M.X] : Œ∑[M].hom = Œ∑[M.X] := rfl
@[simp] lemma hom_Œº (M : Mon_ C) [IsCommMon M.X] : Œº[M].hom = Œº[M.X] := rfl

namespace Hom
variable [IsCommMon N.X]

@[simp] lemma hom_one : (1 : M ‚ü∂ N).hom = 1 := rfl

@[simp] lemma hom_mul (f g : M ‚ü∂ N) : (f * g).hom = f.hom * g.hom := rfl

@[simp] lemma hom_pow (f : M ‚ü∂ N) (n : ‚Ñï) : (f ^ n).hom = f.hom ^ n := by
  induction n <;> simp [pow_succ, *]

end Hom

/-- A commutative monoid object is a commutative monoid object in the category of monoid objects. -/
instance [IsCommMon M.X] : IsCommMon M where mul_comm' := by ext; simp [_root_.mul_comm]

end Mon_
