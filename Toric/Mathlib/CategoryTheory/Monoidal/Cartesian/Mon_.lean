/-
Copyright (c) 2025 Ya√´l Dillies, Micha≈Ç Mruga≈Ça, Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies, Micha≈Ç Mruga≈Ça, Andrew Yang
-/
import Mathlib.CategoryTheory.Adjunction.Limits
import Mathlib.CategoryTheory.Monoidal.Yoneda
import Toric.Mathlib.CategoryTheory.Monoidal.Mon_

open CategoryTheory Limits ChosenFiniteProducts Mon_Class

universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

namespace CategoryTheory.Functor
variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] [ChosenFiniteProducts C]
variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] [ChosenFiniteProducts D]
variable {F F' : C ‚•§ D} [PreservesFiniteProducts F] [PreservesFiniteProducts F']

attribute [local instance] monoidalOfChosenFiniteProducts

/-- Natural transformations between functors lift to monoid objects. -/
@[simps!]
noncomputable def mapMonNatTrans (f : F ‚ü∂ F') : F.mapMon ‚ü∂ F'.mapMon where
  app X := .mk (f.app _)

/-- Natural isomorphisms between functors lift to monoid objects. -/
@[simps!]
noncomputable def mapMonNatIso (e : F ‚âÖ F') : F.mapMon ‚âÖ F'.mapMon :=
  NatIso.ofComponents fun X ‚Ü¶ Mon_.mkIso (e.app _)

end CategoryTheory.Functor

namespace CategoryTheory.Equivalence
variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] [ChosenFiniteProducts C]
variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] [ChosenFiniteProducts D]

attribute [local instance] Functor.monoidalOfChosenFiniteProducts

-- FIXME: There is a diamond between `LaxMonoidal.id` and `Functor.monoidalOfChosenFiniteProducts`
-- noncomputable def mapMon (e : C ‚âå D) : Mon_ C ‚âå Mon_ D where
--   functor := e.functor.mapMon
--   inverse := e.inverse.mapMon
--   unitIso :=
--     Functor.mapMonIdIso.symm ‚â™‚â´ Functor.mapMonNatIso e.unitIso ‚â™‚â´ Functor.mapMonCompIso _ _
--   counitIso :=
--     (Functor.mapMonCompIso _ _).symm ‚â™‚â´ Functor.mapMonNatIso e.counitIso ‚â™‚â´ Functor.mapMonIdIso
--   functor_unitIso_comp := sorry

end CategoryTheory.Equivalence

section

attribute [local instance] monoidOfMon_Class

variable {C : Type*} [Category C] [ChosenFiniteProducts C] {M N : Mon_ C} [IsCommMon N.X]

@[reassoc]
lemma Mon_Class.comp_mul {M N O : C} (f g : M ‚ü∂ N) (h : O ‚ü∂ M) [Mon_Class N] :
    h ‚â´ (f * g) = h ‚â´ f * h ‚â´ g :=
  (((yonedaMon.obj (.mk' N)).map (h.op)).hom.map_mul f g)

lemma Mon_Class.one_eq_one {M : C} [Mon_Class M] : Œ∑ = (1 : _ ‚ü∂ M) := by
  show _ = _ ‚â´ _
  rw [toUnit_unique (toUnit _) (ùüô _), Category.id_comp]

lemma Mon_Class.mul_eq_mul {M : C} [Mon_Class M] : Œº = fst M M * snd _ _ := by
  show _ = _ ‚â´ _
  rw [lift_fst_snd, Category.id_comp]

lemma Mon_.one_eq_one {M : Mon_ C} : M.one = 1 := Mon_Class.one_eq_one (M := M.X)

lemma Mon_.mul_eq_mul {M : Mon_ C} : M.mul = (fst _ _ * snd _ _) := Mon_Class.mul_eq_mul (M := M.X)

@[reassoc]
lemma Mon_Class.mul_comp {M N O : C} (f g : M ‚ü∂ N) (h : N ‚ü∂ O) [Mon_Class N] [Mon_Class O]
    [IsMon_Hom h] :
    (f * g) ‚â´ h = f ‚â´ h * g ‚â´ h := ((yonedaMon.map (.mk' h)).app (.op M)).hom.map_mul f g

@[reassoc (attr := simp)]
lemma Mon_Class.one_comp {M N O : C} (h : N ‚ü∂ O) [Mon_Class N] [Mon_Class O] [IsMon_Hom h] :
    (1 : M ‚ü∂ N) ‚â´ h = 1 := ((yonedaMon.map (.mk' h)).app (.op M)).hom.map_one

@[reassoc (attr := simp)]
lemma Mon_Class.comp_one {M N O : C} (h : M ‚ü∂ N) [Mon_Class O] :
    h ‚â´ (1 : N ‚ü∂ O) = 1 := ((yonedaMon.obj (.mk' O)).map (h.op)).hom.map_one

instance Hom.instCommMonoid {M N : C} [Mon_Class N] [IsCommMon N] : CommMonoid (M ‚ü∂ N) where
  mul_comm f g := by
    show lift _ _ ‚â´ _ = lift _ _ ‚â´ _
    conv_lhs => rw [‚Üê IsCommMon.mul_comm N]
    rw [‚Üê Category.assoc]
    congr 1
    ext <;> simp

@[reassoc]
lemma Mon_Class.comp_pow {M N O : C} (f : M ‚ü∂ N) (n : ‚Ñï) (h : O ‚ü∂ M) [Mon_Class N] :
    h ‚â´ f ^ n = (h ‚â´ f) ^ n := by
  induction' n with n hn
  ¬∑ simp
  simp only [pow_succ, Mon_Class.comp_mul, hn]

end

namespace Mon_.Hom

variable {C : Type*} [Category C] [ChosenFiniteProducts C] {M N : Mon_ C}

attribute [local instance] monoidOfMon_Class

instance instOne : One (M ‚ü∂ N) where
  one.hom := 1
  one.one_hom := by simp [one_eq_one]
  one.mul_hom := by simp [mul_eq_mul, Mon_Class.comp_mul]

lemma hom_one : (1 : (M ‚ü∂ N)).hom = 1 := rfl

variable [IsCommMon N.X]

instance instMul : Mul (M ‚ü∂ N) where
  mul f g := {
    hom := f.hom * g.hom
    one_hom := by simp [Mon_.one_eq_one, Mon_Class.comp_mul, Mon_Class.one_comp]
    mul_hom := by simp [mul_eq_mul, comp_mul, mul_comp, mul_mul_mul_comm]
  }

@[simp]
lemma hom_mul (f g : M ‚ü∂ N) : (f * g).hom = f.hom * g.hom := rfl

instance instPow : Pow (M ‚ü∂ N) ‚Ñï where
  pow f n := {
    hom := f.hom ^ n
    one_hom := by simp [Mon_.one_eq_one, Mon_Class.one_comp, Mon_Class.comp_pow]
    mul_hom := by
      simp [mul_eq_mul, Mon_Class.comp_mul, Mon_Class.mul_comp, Mon_Class.comp_pow, mul_pow]
  }

@[simp] lemma hom_pow (f : M ‚ü∂ N) (n : ‚Ñï) : (f ^ n).hom = f.hom ^ n := rfl

instance : CommMonoid (M ‚ü∂ N) :=
  Function.Injective.commMonoid hom (fun _ _ ‚Ü¶ ext) hom_one hom_mul hom_pow

end Mon_.Hom
