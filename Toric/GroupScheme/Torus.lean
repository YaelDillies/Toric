/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.Algebra.Category.Grp.Adjunctions
import Mathlib.Algebra.Category.Grp.EquivalenceGroupAddGroup
import Mathlib.Algebra.Category.Ring.Adjunctions
import Mathlib.AlgebraicGeometry.Limits
import Toric.Mathlib.Algebra.Category.Grp.Basic
import Toric.Mathlib.Algebra.Category.MonCat.Basic
import Toric.Mathlib.CategoryTheory.Monoidal.Cartesian.CommGrp_
import Toric.MvLaurentPolynomial

/-!
# The standard algebraic torus

This file defines the standard algebraic torus over `Spec R` as `Spec (R âŠ— â„¤[Fâ‚™])`.
-/

noncomputable section

open CategoryTheory Opposite Limits

namespace AlgebraicGeometry.Scheme

def DiagInt (M : Type*) [CommMonoid M] : Scheme := Spec (.of (MonoidAlgebra (ULift â„¤) M))

def DiagInt.representableBy (M : Type*) [CommMonoid M] :
    (Scheme.Î“ â‹™ forgetâ‚‚ _ CommMonCat â‹™
      CommMonCat.coyoneda.obj (op (.of M)) â‹™ forget _).RepresentableBy
      (DiagInt M) :=
  letI e : opOp CommMonCat â‹™ yoneda.obj (op (.of M)) â‰… CommMonCat.coyoneda.obj _ â‹™ forget _ :=
    Coyoneda.opIso.app (op _) â‰ªâ‰« CommMonCat.coyonedaForget.symm.app (op (.of M))
  letI e' := isoWhiskerLeft (Scheme.Î“ â‹™ forgetâ‚‚ _ CommMonCat) e
  ((Î“Spec.adjunction.comp (CommRingCat.forgetâ‚‚Adj CommRingCat.isInitial).op).representableBy
    (op (.of M))).ofIso e'

instance (M : Type*) [CommMonoid M] : Mon_Class (DiagInt M) :=
  Mon_Class.ofRepresentableBy _ (Scheme.Î“ â‹™ forgetâ‚‚ _ CommMonCat â‹™
    CommMonCat.coyoneda.obj (op (.of M)) â‹™ forgetâ‚‚ _ _) (DiagInt.representableBy M)

def TorusInt (Ïƒ : Type*) : Scheme := DiagInt (Multiplicative (FreeAbelianGroup Ïƒ))

def TorusInt.representableBy (Ïƒ : Type*) :
    (Scheme.Î“ â‹™ forgetâ‚‚ _ CommMonCat â‹™ CommMonCat.units â‹™
      CommGrp.coyonedaRight.obj (op Ïƒ) â‹™ forget _).RepresentableBy
        (TorusInt Ïƒ) :=
  ((Î“Spec.adjunction.comp <| (CommRingCat.forgetâ‚‚Adj CommRingCat.isInitial).op.comp <|
    CommGrp.forgetâ‚‚CommMonAdj.op.comp <|
      commGroupAddCommGroupEquivalence.symm.toAdjunction.op.comp <|
        AddCommGrp.adj.op).representableBy (op Ïƒ)).ofIso <|
    isoWhiskerLeft (Scheme.Î“ â‹™ forgetâ‚‚ _ CommMonCat â‹™ CommMonCat.units â‹™ forget CommGrp)
      (Coyoneda.opIso.app _)

instance (Ïƒ : Type*) : CommGrp_Class (TorusInt Ïƒ) :=
  .ofRepresentableBy _
    (Scheme.Î“ â‹™ forgetâ‚‚ _ CommMonCat â‹™ CommMonCat.units â‹™ CommGrp.coyonedaRight.obj (op Ïƒ))
      (TorusInt.representableBy Ïƒ)

attribute [local instance] Functor.Braided.ofChosenFiniteProducts in
def CommGrp_Torus (S : Scheme) (Ïƒ : Type*) : CommGrp_ (Over S) :=
  ((Over.equivalenceOfIsTerminal terminalIsTerminal).inverse â‹™
    Over.pullback (terminal.from _)).mapCommGrp.obj
      (.mk' (TorusInt Ïƒ))

/-- The (split) algebraic torus over `S` indexed by `Ïƒ`. -/
def splitTorus (S : Scheme) (Ïƒ : Type*) : Scheme := (CommGrp_Torus S Ïƒ).X.left

notation3 "ð”¾â‚˜["S"]" => splitTorus S PUnit

example (S : Scheme) (Ïƒ : Type*) :
    splitTorus S Ïƒ = pullback (terminal.from (TorusInt Ïƒ)) (terminal.from S) := rfl

instance splitTorus.instCanonicallyOver (S : Scheme) (Ïƒ : Type*) :
    (splitTorus S Ïƒ).CanonicallyOver S where
  hom := (CommGrp_Torus S Ïƒ).X.hom

instance (S : Scheme) (Ïƒ : Type*) : CommGrp_Class (asOver (splitTorus S Ïƒ) S) :=
  inferInstanceAs (CommGrp_Class (CommGrp_Torus S Ïƒ).X)

def splitTorus.representableBy (S : Scheme) (Ïƒ : Type*) :
    ((Over.forget _).op â‹™ Scheme.Î“ â‹™ forgetâ‚‚ _ CommMonCat â‹™ CommMonCat.units â‹™
      CommGrp.coyonedaRight.obj (op Ïƒ) â‹™ forget _).RepresentableBy
      (Over.mk (splitTorus S Ïƒ â†˜ S)) :=
  ((((Over.mapPullbackAdj (terminal.from S)).comp
    (Over.equivalenceOfIsTerminal terminalIsTerminal).toAdjunction).comp <|
    (Î“Spec.adjunction.comp <| (CommRingCat.forgetâ‚‚Adj CommRingCat.isInitial).op.comp <|
      CommGrp.forgetâ‚‚CommMonAdj.op.comp <|
        commGroupAddCommGroupEquivalence.symm.toAdjunction.op.comp <|
          AddCommGrp.adj.op)).representableBy (op Ïƒ)).ofIso <|
    isoWhiskerRight (NatIso.op (Over.forgetMapTerminal _ _))
      (Scheme.Î“ â‹™ forgetâ‚‚ _ CommMonCat â‹™
        CommMonCat.units â‹™ forget _ â‹™ opOp _ â‹™ yoneda.obj (op Ïƒ)) â‰ªâ‰«
        (isoWhiskerLeft ((Over.forget _).op â‹™ Scheme.Î“ â‹™ forgetâ‚‚ _ CommMonCat â‹™
          CommMonCat.units â‹™ forget CommGrp) (Coyoneda.opIso.app _))

/-- The split torus of dimension `Ïƒ` over `Spec R`. -/
notation "ð”¾â‚˜[" R ", " Ïƒ "]" => asOver (splitTorus (Spec R) Ïƒ) (Spec R)

/-- The split torus with dimensions `Ïƒ` over `Spec R` is isomorphic to `Spec R[â„¤^Ïƒ]`. -/
def splitTorusIsoSpec (R : CommRingCat) (Ïƒ : Type*) :
    splitTorus (Spec R) Ïƒ â‰… Spec (.of <| MvLaurentPolynomial Ïƒ R) := sorry

/-- The split torus of dimension `n` over `Spec R` is isomorphic to `Spec R[â„¤â¿]`. -/
def splitTorusIsoSpecOver (R : CommRingCat) (Ïƒ : Type*) :
    ð”¾â‚˜[R, Ïƒ] â‰… .mk <| Spec.map <| CommRingCat.ofHom <| algebraMap R (MvLaurentPolynomial Ïƒ R) :=
  Over.isoMk (splitTorusIsoSpec _ _) sorry

end AlgebraicGeometry.Scheme
