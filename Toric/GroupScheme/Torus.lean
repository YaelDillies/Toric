/-
Copyright (c) 2025 YaÃ«l Dillies, MichaÅ‚ MrugaÅ‚a, Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, MichaÅ‚ MrugaÅ‚a, Andrew Yang
-/
import Mathlib.FieldTheory.Separable
import Toric.GroupScheme.Diagonalizable
import Toric.MvLaurentPolynomial
import Toric.Mathlib.CategoryTheory.Monoidal.Cartesian.Grp_

/-!
# The standard algebraic torus

This file defines the standard algebraic torus over `Spec R` as `Spec (R âŠ— â„¤[Fâ‚™])`.
-/

noncomputable section

open CategoryTheory Opposite Limits MonoidalCategory

namespace AlgebraicGeometry.Scheme

universe u v

section IsSplitTorus
variable {S G H : Scheme.{u}} [G.Over S] [H.Over S] [Grp_Class (asOver G S)]
  [Grp_Class (asOver H S)]

variable (S G) in
@[mk_iff]
class IsSplitTorus : Prop where
  existsIso :
    âˆƒ (A : Type u) (_ : AddCommGroup A) (_ : Module.Free â„¤ A),
      Nonempty <| Grp_.mk' (asOver G S) â‰… .mk' (asOver (Diag S A) S)

instance {A : Type u} [AddCommGroup A] [Module.Free â„¤ A] : IsSplitTorus S (Diag S A) :=
  âŸ¨A, â€¹_â€º, â€¹_â€º, âŸ¨by exact .refl _âŸ©âŸ©

lemma IsSplitTorus.of_iso [IsSplitTorus S H]
    (e : Grp_.mk' (asOver G S) â‰… .mk' (asOver H S)) : IsSplitTorus S G :=
  let âŸ¨A, _, _, âŸ¨e'âŸ©âŸ© := â€¹IsSplitTorus S Hâ€º; âŸ¨A, _, â€¹_â€º, âŸ¨e.trans e'âŸ©âŸ©

instance  (f : G âŸ¶ H) [IsIso f] [f.IsOver S] : IsIso (f.asOver S) :=
  have : IsIso ((Over.forget S).map (Hom.asOver f S)) := â€¹_â€º
  isIso_of_reflects_iso _ (Over.forget _)

lemma IsSplitTorus.of_isIso [IsSplitTorus S H]
    (f : G âŸ¶ H) [IsIso f] [f.IsOver S] [IsMon_Hom (f.asOver S)] : IsSplitTorus S G :=
  have : IsMon_Hom (asIso (Hom.asOver f S)).hom := â€¹_â€º
  .of_iso (H := H) ((Grp_.fullyFaithfulForgetâ‚‚Mon_ _).preimageIso
    (Mon_.mkIso' (asIso (f.asOver S))))

lemma IsSplitTorus.of_isIso' [IsSplitTorus S G]
    (f : G âŸ¶ H) [IsIso f] [f.IsOver S] [IsMon_Hom (f.asOver S)] : IsSplitTorus S H :=
  have : IsMon_Hom (asIso (Hom.asOver f S)).hom := â€¹_â€º
  .of_iso (H := G) ((Grp_.fullyFaithfulForgetâ‚‚Mon_ _).preimageIso
    (.symm <| Mon_.mkIso' (asIso (f.asOver S))))

end IsSplitTorus

section IsTorus
variable {k : Type u} [Field k] {G H : Scheme.{u}} [G.Over (Spec (.of k))] [H.Over (Spec (.of k))]
  [Grp_Class (asOver G (Spec (.of k)))] [Grp_Class (asOver H (Spec (.of k)))]

variable (k G) in
@[mk_iff]
class IsTorus : Prop where
  existsSplit :
    âˆƒ (L : Type u) (_ : Field L) (_ : Algebra k L) (_ : Algebra.IsSeparable k L),
      IsSplitTorus (Spec (.of L)) <|
        pullback (G â†˜ Spec (.of k)) (Spec.map (CommRingCat.ofHom <| algebraMap k L))

instance [IsSplitTorus (Spec (.of k)) G] : IsTorus k G :=
  âŸ¨âŸ¨k, â€¹_â€º, inferInstance, inferInstance, by
    simp only [Algebra.id.map_eq_id, CommRingCat.ofHom_id]
    rw [Spec.map_id]
    exact .of_isIso (pullback.fst (G â†˜ (Spec (.of k))) (ğŸ™ (Spec (.of k))))âŸ©âŸ©

lemma IsTorus.of_iso [IsTorus k H]
    (e : Grp_.mk' (asOver G (Spec (.of k))) â‰… .mk' (asOver H (Spec (.of k)))) : IsTorus k G :=
  let âŸ¨L, _, _, _, hHâŸ© := â€¹IsTorus k Hâ€º; âŸ¨L, _, â€¹_â€º, â€¹_â€º,
    have e := (Over.pullback (Spec.map (CommRingCat.ofHom (algebraMap k L)))).mapGrp.mapIso e
    .of_iso (H := (pullback (H â†˜ Spec (.of k))
      (Spec.map (CommRingCat.ofHom <| algebraMap k L)))) (by convert e using 1)âŸ©

lemma IsTorus.of_isIso [IsTorus k H]
    (f : G âŸ¶ H) [IsIso f] [f.IsOver (Spec (.of k))] [IsMon_Hom (f.asOver (Spec (.of k)))] :
    IsTorus k G :=
  have : IsMon_Hom (asIso (Hom.asOver f (Spec (.of k)))).hom := â€¹_â€º
  .of_iso (H := H) ((Grp_.fullyFaithfulForgetâ‚‚Mon_ _).preimageIso
    (Mon_.mkIso' (asIso (f.asOver (Spec (.of k))))))

end IsTorus

section
/-- The (split) algebraic torus over `S` indexed by `Ïƒ`. -/
abbrev SplitTorus (S : Scheme) (Ïƒ : Type u) : Scheme.{u} := Diag S <| FreeAbelianGroup Ïƒ

@[inherit_doc SplitTorus]
notation3 "ğ”¾â‚˜[" S ", " Ïƒ "]" => SplitTorus S Ïƒ

/-- The multiplicative group over `S`. -/
notation3 "ğ”¾â‚˜["S"]" => ğ”¾â‚˜[S, PUnit]

-- def SplitTorus.representableBy (S : Scheme) (Ïƒ : Type*) :
--     ((Over.forget _).op â‹™ Scheme.Î“ â‹™ forgetâ‚‚ _ CommMonCat â‹™ CommMonCat.units â‹™
--       CommGrp.coyonedaRight.obj (op Ïƒ) â‹™ forget _).RepresentableBy
--       (ğ”¾â‚˜[S, Ïƒ].asOver S) :=
--   ((((Over.mapPullbackAdj (terminal.from S)).comp
--     (Over.equivalenceOfIsTerminal terminalIsTerminal).toAdjunction).comp <|
--     (Î“Spec.adjunction.comp <| (CommRingCat.forgetâ‚‚Adj CommRingCat.isInitial).op.comp <|
--       CommGrp.forgetâ‚‚CommMonAdj.op.comp <|
--         commGroupAddCommGroupEquivalence.symm.toAdjunction.op.comp <|
--           AddCommGrp.adj.op)).representableBy (op Ïƒ)).ofIso <|
--     isoWhiskerRight (NatIso.op (Over.forgetMapTerminal _ _))
--       (Scheme.Î“ â‹™ forgetâ‚‚ _ CommMonCat â‹™
--         CommMonCat.units â‹™ forget _ â‹™ opOp _ â‹™ yoneda.obj (op Ïƒ)) â‰ªâ‰«
--         (isoWhiskerLeft ((Over.forget _).op â‹™ Scheme.Î“ â‹™ forgetâ‚‚ _ CommMonCat â‹™
--           CommMonCat.units â‹™ forget CommGrp) (Coyoneda.opIso.app _))

variable {R : CommRingCat} {Ïƒ : Type*}

variable (R Ïƒ) in
/-- The split torus with dimensions `Ïƒ` over `Spec R` is isomorphic to `Spec R[â„¤^Ïƒ]`. -/
abbrev splitTorusIso (R : CommRingCat) (Ïƒ : Type*) :
    ğ”¾â‚˜[Spec R, Ïƒ] â‰… Spec (.of <| MvLaurentPolynomial Ïƒ R) := diagSpecIso _ _

end

section Product
variable {S G H : Scheme.{u}} [G.Over S] [H.Over S] [Grp_Class (asOver G S)]
  [Grp_Class (asOver H S)]

@[simp]
lemma leftAsOver {X : Over S} : asOver X.left S = X := rfl

instance {X : Over S} [Grp_Class X] : Grp_Class <| asOver X.left S := by simpa

instance IsSplitTorus.product [IsSplitTorus S G] [IsSplitTorus S H] :
    IsSplitTorus S <| (asOver G S âŠ— asOver H S).left := sorry

end Product

end AlgebraicGeometry.Scheme
