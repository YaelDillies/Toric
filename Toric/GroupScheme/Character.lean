/-
Copyright (c) 2025 YaÃ«l Dillies, MichaÅ‚ MrugaÅ‚a, Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, MichaÅ‚ MrugaÅ‚a, Andrew Yang
-/
import Mathlib.Algebra.FreeAbelianGroup.Finsupp
import Toric.GroupScheme.Torus
import Toric.Mathlib.Algebra.Group.Equiv.Basic
import Toric.Mathlib.GroupTheory.FreeAbelianGroup
import Toric.Mathlib.LinearAlgebra.PerfectPairing.Basic

/-!
# The lattices of characters and cocharacters
-/

noncomputable section

open AddMonoidAlgebra CategoryTheory
open scoped Hom

namespace AlgebraicGeometry.Scheme
universe u

section general_base
variable {Ïƒ : Type u} {S G H : Scheme.{u}} [G.Over S] [H.Over S]

section Grp_Class
variable [Grp_Class (G.asOver S)] [Grp_Class (H.asOver S)]

variable (S G) in
/-- The characters of the group scheme `G` over `S` are the group morphisms `G âŸ¶/S ð”¾â‚˜[S]`. -/
abbrev Char := HomGrp G ð”¾â‚˜[S] S

variable (S G) in
/-- The cocharacters of the group scheme `G` over `S` are the group morphisms `ð”¾â‚˜[S] âŸ¶/S G`. -/
abbrev Cochar := HomGrp ð”¾â‚˜[S] G S

@[inherit_doc] notation "X("S", "G")" => Char S G
@[inherit_doc] notation "X*("S", "G")" => Cochar S G

variable (S) in
/-- Characters of isomorphic group schemes are isomorphic. -/
def charCongr (e : G â‰… H) [e.hom.IsOver S] [IsMon_Hom <| e.hom.asOver S] : X(S, G) â‰ƒ+ X(S, H) :=
  HomGrp.congr e (.refl _)

@[simp]
lemma charCongr_symm (e : G â‰… H) [e.hom.IsOver S] [IsMon_Hom <| e.hom.asOver S] :
  (charCongr S e).symm = charCongr S e.symm := rfl

variable (S) in
/-- Cocharacters of isomorphic commutative group schemes are isomorphic. -/
def cocharCongr [IsCommMon <| G.asOver S] [IsCommMon <| H.asOver S]
    (e : G â‰… H) [e.hom.IsOver S] [IsMon_Hom <| e.hom.asOver S] : X*(S, G) â‰ƒ+ X*(S, H) :=
  HomGrp.congr (.refl _) e

@[simp]
lemma cocharCongr_symm (e : G â‰… H) [IsCommMon <| G.asOver S] [IsCommMon <| H.asOver S]
    [e.hom.IsOver S] [IsMon_Hom <| e.hom.asOver S] :
  (cocharCongr S e).symm = cocharCongr S e.symm := rfl

@[simp]
lemma cocharCongr_comp_charCongr [IsCommMon <| G.asOver S] [IsCommMon <| H.asOver S]
    (e : G â‰… H) [e.hom.IsOver S] [IsMon_Hom <| e.hom.asOver S] (a b) :
    (cocharCongr S e a).comp (charCongr S e b) = a.comp b := by
  ext
  simp [charCongr, cocharCongr, HomGrp.congr]

end Grp_Class

section CommGrp_Class
variable [CommGrp_Class (G.asOver S)]

/-- The perfect pairing between characters and cocharacters, valued in the characters of the
algebraic torus. -/
@[simps]
noncomputable def charPairingAux : X*(S, G) â†’+ X(S, G) â†’+ X(S, ð”¾â‚˜[S]) where
  toFun Ï‡ :=
  { toFun Ï‡' := Ï‡.comp Ï‡', map_zero' := by simp, map_add' := by simp [HomGrp.add_comp] }
  map_zero' := by ext f; simp
  map_add' Ï‡ Ï‡' := by ext; simp [HomGrp.comp_add]

end CommGrp_Class
end general_base

section IsDomain
variable {R : CommRingCat.{u}} [IsDomain R] {Ïƒ : Type u} {G T : Scheme.{u}} [G.Over (Spec R)]
  [T.Over (Spec R)]

section AddCommGroup
variable {G : Type u} [AddCommGroup G]

variable (R G) in
/-- Characters of a diagonal group scheme over a domain are exactly the input group.

Note: This is true over a general base using Cartier duality, but we do not prove that. -/
def charDiag : X(Spec R, Diag (Spec R) G) â‰ƒ+ G :=
  diagHomEquiv.symm.trans <| FreeAbelianGroup.liftAddEquiv.symm.trans <| .piUnique fun _ â†¦ G

lemma charDiag_symm_apply (g : G) :
    (charDiag R G).symm g = diagHomGrp _ (FreeAbelianGroup.lift fun _ â†¦ g) := rfl

lemma charDiag_diagHomGrp (f : _ â†’+ G) : charDiag R G (diagHomGrp _ f) = f (.of 0) := by
  apply (charDiag R G).symm.injective
  simp [charDiag_symm_apply]
  congr 1
  ext
  simp only [FreeAbelianGroup.lift.of]

variable (R G) in
/-- Cocharacters of a diagonal group scheme over a domain are exactly the dual of the input group.

Note: This is true over a general base using Cartier duality, but we do not prove that. -/
def cocharDiag : X*(Spec R, Diag (Spec R) G) â‰ƒ+ (G â†’+ â„¤) :=
  diagHomEquiv.symm.trans <| AddMonoidHom.postcompAddEquiv <| FreeAbelianGroup.uniqueEquiv _

lemma cocharDiag_symm_apply (g : G â†’+ â„¤) :
  (cocharDiag R G).symm g =
    diagHomGrp _ ((FreeAbelianGroup.uniqueEquiv _).symm.toAddMonoidHom.comp g) := rfl

end AddCommGroup

variable (R Ïƒ) in
/-- Characters of the algebraic torus with dimensions `Ïƒ`over a domain `R` are exactly `â„¤^Ïƒ`.

Note: This is true over a general base using Cartier duality, but we do not prove that. -/
def charTorus : X(Spec R, ð”¾â‚˜[Spec R, Ïƒ]) â‰ƒ+ (Ïƒ â†’â‚€ â„¤) :=
  (charDiag R _).trans (FreeAbelianGroup.equivFinsupp _)

variable (R) in
def charTorusUnit : X(Spec R, ð”¾â‚˜[Spec R]) â‰ƒ+ â„¤ :=
  (charDiag R _).trans (FreeAbelianGroup.uniqueEquiv _)

-- variable (R Ïƒ) in
-- /-- Cocharacters of the algebraic torus with dimensions `Ïƒ`over a domain `R` are exactly `â„¤^Ïƒ`.

-- Note: This is true over a general base using Cartier duality, but we do not prove that. -/
-- def cocharTorus : X*(Spec R, ð”¾â‚˜[Spec R, Ïƒ]) â‰ƒ+ (Ïƒ â†’ â„¤) :=
--   (cocharDiag R _).trans âŸ¨FreeAbelianGroup.lift.symm, fun _ _ â†¦ rflâŸ©

section CommGrp_Class
variable [CommGrp_Class (G.asOver (Spec R))] [CommGrp_Class (T.asOver (Spec R))]

variable (R G) in
attribute [local instance 1000000] AddEquivClass.instAddHomClass AddMonoidHomClass.toAddHomClass
  AddEquivClass.instAddMonoidHomClass in
attribute [-simp] charPairingAux_apply_apply in
/-- The `â„¤`-valued perfect pairing between characters and cocharacters of group schemes over a
domain.

Note: This exists over a general base using Cartier duality, but we do not prove that.  -/
noncomputable def charPairing : X*(Spec R, G) â†’â‚—[â„¤] X(Spec R, G) â†’â‚—[â„¤] â„¤ where
  toFun x :=
  { toFun y := charTorusUnit (R := R) (charPairingAux (S := Spec R) (G := G) x y)
    map_add' _ _ := by simp only [map_add]
    map_smul' _ _ := by simp only [map_zsmul, smul_eq_mul, eq_intCast, Int.cast_eq] }
  map_add' _ _ := by ext; simp only [map_add, AddMonoidHom.add_apply, LinearMap.coe_mk,
    AddHom.coe_mk, LinearMap.add_apply]
  map_smul' _ _ := by ext; simp only [map_zsmul, AddMonoidHom.coe_smul, Pi.smul_apply, smul_eq_mul,
    LinearMap.coe_mk, AddHom.coe_mk, eq_intCast, Int.cast_eq, LinearMap.smul_apply]

set_option maxHeartbeats 0 in
instance isPerfPair_charPairing [T.IsSplitTorusOver Spec(R)] [LocallyOfFiniteType (T â†˜ Spec(R))] :
    (charPairing R T).IsPerfPair := by
  obtain âŸ¨Ïƒ, _, e, _, _âŸ© := exists_iso_diag_finite_of_isSplitTorusOver_locallyOfFiniteType T Spec(R)
  refine .congr (.id (R := â„¤) (M := Module.Dual â„¤ (Ïƒ â†’â‚€ â„¤)))
    ((cocharCongr _ e).trans ((cocharDiag (.of R) â„¤[Ïƒ]).trans
      (addMonoidHomLequivInt â„¤).toAddEquiv)).toIntLinearEquiv
    ((charCongr _ e).trans <| charDiag (.of R) â„¤[Ïƒ]).toIntLinearEquiv _ ?_
  ext f x
  apply (charTorusUnit (R := R)).symm.injective
  apply Additive.ofMul.symm.injective
  dsimp [charDiag_symm_apply, charPairing, charTorusUnit, charTorus,
    cocharDiag_symm_apply, AddMonoidAlgebra]
  simp only [Char, cocharCongr_comp_charCongr, diagHomGrp_comp, charDiag_diagHomGrp, PUnit.zero_eq,
    AddMonoidHom.coe_comp, AddMonoidHom.coe_coe, Function.comp_apply, FreeAbelianGroup.lift.of,
    AddEquiv.symm_apply_apply, EmbeddingLike.apply_eq_iff_eq]
  rfl

end CommGrp_Class
end IsDomain
end AlgebraicGeometry.Scheme
