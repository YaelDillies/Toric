/-
Copyright (c) 2025 Paul Reichert, Justus Springer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Paul Reichert, Justus Springer
-/
import Toric.Mathlib.Analysis.Convex.Cone.Pointed
import Mathlib.Analysis.Convex.Cone.Pointed
import Mathlib.Analysis.Convex.Extreme
import Mathlib.Analysis.Convex.Exposed

/-!
# Pointed cone hull and polyhedral cones

We define the pointed cone hull and what it means for a pointed cone to be polyhedral.
-/

variable {ğ•œ R E : Type*}

open scoped InnerProductSpace

namespace PointedCone
section OrderedSemiring
variable [Semiring R] [PartialOrder R] [IsOrderedRing R] [AddCommMonoid E] [Module R E] {s : Set E}

theorem span_le (c : PointedCone R E) {s : Set E} :
    span R s â‰¤ c â†” s âŠ† c :=
  Submodule.span_le

/-- A pointed cone is polyhedral if it is the convex hull of finitely many points. -/
def IsPolyhedral (c : PointedCone R E) : Prop := âˆƒ t : Finset E, PointedCone.span R t = c

protected lemma IsPolyhedral.span (h : s.Finite) : (span R s).IsPolyhedral := âŸ¨h.toFinset, by simpâŸ©
def isPolyhedral_iff_eq_span (c : PointedCone R E) :
    c.IsPolyhedral â†” âˆƒ t : Finset E, c = PointedCone.span R (t âˆª {0}) := by
  apply Iff.intro
  Â· rintro âŸ¨g, hgâŸ©
    refine âŸ¨g, ?_âŸ©
    apply le_antisymm
    Â· simp [hg]
    Â· simp only [Set.union_singleton, Submodule.span_insert_zero, hg, le_refl]
  Â· rintro âŸ¨g, hgâŸ©
    refine âŸ¨open Classical in g âˆª {0}, ?_âŸ©
    simp_all

@[simp] lemma IsPolyhedral.bot : (âŠ¥ : PointedCone R E).IsPolyhedral := âŸ¨{0}, by simpâŸ©

end OrderedSemiring

section LinearOrderedField
variable [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [AddCommGroup E] [Module ğ•œ E]

/-- `âŠ¤` is a polyhedral cone in a finite dimensional vector space over a linear ordered field. -/
@[simp]
lemma IsPolyhedral.top [hE : FiniteDimensional ğ•œ E] : (âŠ¤ : PointedCone ğ•œ E).IsPolyhedral := by
  classical
  obtain âŸ¨S, hSâŸ© := Module.finite_def.mp hE
  -- We take R to be the union of S with {-x | x âˆˆ S}
  let R : Finset E := S âˆª S.map (Function.Embedding.mk (Neg.neg : E â†’ E) neg_injective)
  -- We first show that the span of R is closed under negation
  have neg_mem_span_R : âˆ€ x âˆˆ span ğ•œ R, (-x : E) âˆˆ span ğ•œ R := by
    apply Submodule.span_induction
    Â· intro x hx
      apply Submodule.subset_span
      -- Clearly, T is closed under negation. We show this by simple case distinction
      rw [Finset.mem_coe, Finset.mem_union] at hx
      cases' hx with hxâ‚ hxâ‚‚
      Â· apply Finset.mem_union_right
        simpa only [Finset.mem_map, Function.Embedding.coeFn_mk, neg_inj, exists_eq_right]
      Â· rw [Finset.mem_map, Function.Embedding.coeFn_mk] at hxâ‚‚
        obtain âŸ¨y, hy1, rflâŸ© := hxâ‚‚
        rw [neg_neg]
        exact Finset.mem_union_left _ hy1
    -- The three other cases in the induction are trivial
    Â· simp
    Â· intro x y _ _ hx hy
      rw [neg_add_rev]
      exact Submodule.add_mem _ hy hx
    Â· intro t x _ hx
      rw [â†smul_neg]
      exact Submodule.smul_mem _ _ hx
  -- We now claim that `âŠ¤` is generated as a pointed cone by `R`.
  use R
  rw [Submodule.eq_top_iff']
  rw [Submodule.eq_top_iff'] at hS
  intro x
  specialize hS x
  revert hS x
  -- By reverting x, the claim now says that every element of the span of S
  -- (as a usual `â„`-submodule) is contained in the span of `R` as a pointed cone.
  -- This can be shown by induction on the span.
  apply Submodule.span_induction
  Â· intro x hxS
    apply Submodule.subset_span
    exact Finset.mem_union_left _ hxS
  Â· apply Submodule.zero_mem
  Â· intro x y _ _ hx hy
    exact Submodule.add_mem _ hx hy
  Â· intro t x _ hx
    -- This is the only interesting case, as here we have split cases
    -- according to whether the scalar `t` is positive or not.
    by_cases ht : 0 â‰¤ t
    Â· exact Submodule.smul_mem _ âŸ¨t, htâŸ© hx
    Â· rw [â† neg_neg (t â€¢ x), â† neg_smul, â† smul_neg]
      apply Submodule.smul_mem _ (âŸ¨-t, by linarithâŸ© : {a : ğ•œ // 0 â‰¤ a})
      -- We use our auxiliary statement from above
      exact neg_mem_span_R _ hx

end LinearOrderedField

section NormedAddCommGroup
variable [NormedAddCommGroup E] [InnerProductSpace â„ E] {S : Finset E} {w x y : E}

/-- A generating set for `span â„ S âŠ“ dual' {w}`, see `span_inf_dual'_singleton_eq_span -/
private noncomputable abbrev infDualSingletonGenSet (S : Finset E) (w : E) : Finset E :=
  open scoped Classical in
  {s âˆˆ S | 0 â‰¤ âŸªs, wâŸ«_â„} âˆª
    .imageâ‚‚ (fun x y => âŸªx, wâŸ«_â„ â€¢ y - âŸªy, wâŸ«_â„ â€¢ x) {s âˆˆ S | 0 â‰¤ âŸªs, wâŸ«_â„} {s âˆˆ S | âŸªs, wâŸ«_â„ â‰¤ 0}

private lemma mem_span_infDualSingletonGenSet (hx : x âˆˆ span â„ {s âˆˆ S | 0 â‰¤ âŸªs, wâŸ«_â„})
    (hy : y âˆˆ span â„ {s âˆˆ S | âŸªs, wâŸ«_â„ â‰¤ 0}) :
    âŸªx, wâŸ«_â„ â€¢ y - âŸªy, wâŸ«_â„ â€¢ x âˆˆ span â„ (infDualSingletonGenSet S w) := by
  classical
  induction hx, hy using Submodule.span_inductionâ‚‚ with
  | mem_mem x y hx hy =>
    apply Submodule.subset_span
    apply Finset.subset_union_right
    simpa using âŸ¨x, hx, y, hy, rflâŸ©
  | zero_left x hx =>
    simp only [inner_zero_left, zero_smul, smul_zero, sub_self, Submodule.zero_mem]
  | zero_right x hx =>
    simp only [smul_zero, inner_zero_left, zero_smul, sub_self, Submodule.zero_mem]
  | add_left x y z hx hy hz hxz hyz =>
    convert Submodule.add_mem _ hxz hyz using 1
    rw [inner_add_left, smul_add, add_smul]
    abel
  | add_right x y z hx hy hz hxy hxz =>
    convert Submodule.add_mem _ hxy hxz using 1
    rw [inner_add_left, smul_add, add_smul]
    abel
  | smul_left t x y hx hy hxy =>
    convert Submodule.smul_mem _ t hxy using 1
    rw [Nonneg.mk_smul, real_inner_smul_left, Nonneg.mk_smul, smul_sub, smul_smul,
      smul_smul, smul_smul]
    nth_rw 2 [mul_comm]
  | smul_right t x y hx hy hxy =>
    convert Submodule.smul_mem _ t hxy using 1
    rw [Nonneg.mk_smul, real_inner_smul_left, Nonneg.mk_smul, smul_sub, smul_smul,
      smul_smul, smul_smul, mul_comm]

variable (S w) in
private lemma span_infDualSingletonGenSet :
    span â„ (infDualSingletonGenSet S w) = span â„ S âŠ“ dual' {w} := by
  classical
  apply le_antisymm
  Â· rw [Submodule.span_le]
    apply le_inf
    Â· intro v hv
      simp only [Finset.coe_union, Finset.coe_filter, Finset.coe_imageâ‚‚, Set.mem_union,
        Set.mem_setOf_eq, Set.mem_image2] at hv
      obtain (hv | âŸ¨x, âŸ¨hxS, hxwâŸ©, y, âŸ¨hyS, hywâŸ©, rflâŸ©) := hv
      Â· exact Submodule.subset_span hv.1
      Â· let tâ‚ : {t : â„ // 0 â‰¤ t} := âŸ¨âŸªx, wâŸ«_â„, hxwâŸ©
        let tâ‚‚ : {t : â„ // 0 â‰¤ t} := âŸ¨-âŸªy, wâŸ«_â„, neg_nonneg.mpr hywâŸ©
        rw [SetLike.mem_coe, sub_eq_add_neg, â† neg_smul]
        exact add_mem
          (Submodule.smul_mem _ tâ‚ (Submodule.subset_span hyS))
          (Submodule.smul_mem _ tâ‚‚ (Submodule.subset_span hxS))
    Â· intro x hx
      simp only [Finset.coe_union, Finset.coe_filter, Finset.coe_imageâ‚‚, Set.mem_union,
        Set.mem_setOf_eq, Set.mem_image2] at hx
      obtain (âŸ¨hxS, hxwâŸ© | âŸ¨x, âŸ¨hxS, hxwâŸ©, y, âŸ¨hyS, hywâŸ©, rflâŸ©) := hx
      Â· simp only [SetLike.mem_coe, mem_dual', Set.mem_singleton_iff, forall_eq]
        rw [real_inner_comm]
        exact hxw
      Â· simp only [SetLike.mem_coe, mem_dual', Set.mem_singleton_iff, forall_eq]
        rw [inner_sub_right, real_inner_smul_right, real_inner_smul_right, mul_comm,
          real_inner_comm]
        nth_rw 2 [real_inner_comm]
        rw [sub_self]
  Â· intro v âŸ¨hâ‚, hâ‚‚âŸ©
    simp only [SetLike.mem_coe, mem_dual', Set.mem_singleton_iff, forall_eq] at hâ‚‚
    -- We split `S` into the subset of elements with inner product nonnegative resp. negative.
    have S_eq_union : S = {s âˆˆ S | 0 â‰¤ âŸªs, wâŸ«_â„} âˆª {s âˆˆ S | âŸªs, wâŸ«_â„ < 0} := by
      simp [â† Finset.filter_or, le_or_lt]
    rw [S_eq_union, Finset.coe_union, PointedCone.span, Submodule.span_union,
      SetLike.mem_coe, Submodule.mem_sup] at hâ‚
    -- Let's write `v` as `x+y`, where `x` is in the span of elements with nonnegative
    -- inner product with `w` and `y` is in the span of elements with negative inner product
    -- with `w`.
    obtain âŸ¨x, hx, y, hy, hvâŸ© := hâ‚
    rw [real_inner_comm, â† hv, inner_add_left] at hâ‚‚
    have x_mem : x âˆˆ span â„ (infDualSingletonGenSet S w) :=
      Submodule.span_mono Finset.subset_union_left hx
    -- Clearly, `x` itself has nonnegative inner product with `w`, while `y` has negative
    -- inner product
    simp only [Finset.coe_filter] at hx hy
    have hxw : 0 â‰¤ âŸªx, wâŸ«_â„ := inner_nonneg_of_mem_span_inner_nonneg (fun z hz => hz.2) hx
    have hyw : âŸªy, wâŸ«_â„ â‰¤ 0 := inner_nonpos_of_mem_span_inner_nonpos (fun z hz => hz.2.le) hy
    -- We treat the case `âŸªx, wâŸ«_â„` = 0 seperately.
    by_cases H : âŸªx, wâŸ«_â„ = 0
    Â· rw [H, zero_add] at hâ‚‚
      rw [â† hv]
      apply Submodule.add_mem _ x_mem
      convert Submodule.zero_mem _
      -- Since `y` is in the span of elements with negative inner product with `w`, but itself
      -- has `âŸªy, wâŸ«_â„ = 0`, `y` must be zero.
      exact eq_zero_of_inner_eq_zero_of_mem_span_inner_neg (fun x hx => hx.2) hy <|
        le_antisymm hyw hâ‚‚
    Â· let u : E := âŸªx, wâŸ«_â„ â€¢ y - âŸªy, wâŸ«_â„ â€¢ x
      have u_mem : u âˆˆ span â„ (infDualSingletonGenSet S w) :=
        mem_span_infDualSingletonGenSet hx <|
          Submodule.span_mono (fun z hz => And.intro hz.1 (le_of_lt hz.2)) hy
      have tâ‚‚_nonneg : 0 â‰¤ (âŸªx, wâŸ«_â„)â»Â¹ := inv_nonneg_of_nonneg hxw
      have tâ‚_nonneg : 0 â‰¤ 1 + âŸªy, wâŸ«_â„ * (âŸªx, wâŸ«_â„)â»Â¹ := by
        convert mul_le_mul_of_nonneg_right hâ‚‚ tâ‚‚_nonneg using 1
        Â· rw [zero_mul]
        Â· rw [add_mul, mul_inv_cancelâ‚€ H]
      let tâ‚ : {t : â„ // 0 â‰¤ t} := âŸ¨_, tâ‚_nonnegâŸ©
      let tâ‚‚ : {t : â„ // 0 â‰¤ t} := âŸ¨_, tâ‚‚_nonnegâŸ©
      -- With the above definitions, a computation shows that `v = tâ‚ â€¢ x + tâ‚‚ â€¢ y`.
      have v_eq : v = tâ‚ â€¢ x + tâ‚‚ â€¢ u := by rw [Nonneg.mk_smul, Nonneg.mk_smul, add_smul,
        smul_sub, smul_smul, inv_mul_cancelâ‚€ H, smul_smul, mul_comm, add_add_sub_cancel,
        one_smul, one_smul, hv]
      rw [v_eq]
      -- But both `x` and `u` are in the span and `tâ‚` and `tâ‚‚` are nonnegativedd hence
      -- we are done.
      exact Submodule.add_mem _ (Submodule.smul_mem _ _ x_mem) (Submodule.smul_mem _ _ u_mem)

lemma IsPolyhedral.inf_dual'_singleton {c : PointedCone â„ E} (hc : c.IsPolyhedral) :
    IsPolyhedral (c âŠ“ dual' {w}) := by
  obtain âŸ¨S, rflâŸ© := hc; exact âŸ¨infDualSingletonGenSet S w, span_infDualSingletonGenSet _ _âŸ©

/-- The dual of a polyhedral cone is polyhedral. -/
lemma IsPolyhedral.dual [FiniteDimensional â„ E] {c : PointedCone â„ E} (hc : c.IsPolyhedral) :
    c.dual.IsPolyhedral := by
  classical
  obtain âŸ¨S, rflâŸ© := hc
  rw [dual_span]
  induction' S using Finset.induction with x S hx hS
  Â· simp
  Â· rw [Finset.insert_eq, Finset.coe_union, dual'_union, inf_comm, Finset.coe_singleton]
    exact hS.inf_dual'_singleton

end NormedAddCommGroup

variable {ğ•œ E : Type*} [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
  [AddCommGroup E] [Module ğ•œ E]

theorem smul_mem_openSegment {a b c : ğ•œ} {x : E} (hab : a < b) (hbc : b < c) :
    b â€¢ x âˆˆ openSegment ğ•œ (a â€¢ x) (c â€¢ x) := by
  have hba : b - a > 0 := by simp_all
  have hcb : c - b > 0 := by simp_all
  have hca : c - a > 0 := by simpa using add_pos hba hcb
  refine âŸ¨(c - b) / (c - a), (b - a) / (c - a), by positivity, by positivity, ?_, ?_âŸ©
  Â· rw [â† add_div, sub_add_sub_cancel, div_self]
    positivity
  Â· simp only [smul_smul, smul_smul, â† add_smul, mul_comm, mul_div, â† add_div]
    congr
    rw [div_eq_iff (by positivity)]
    ring

theorem smul_mem_of_isExtreme {c : PointedCone ğ•œ E} {s : Set E} (he : IsExtreme ğ•œ c s) :
    âˆ€ x âˆˆ s, âˆ€ a : ğ•œ, a â‰¥ 0 â†’ a â€¢ x âˆˆ s := by
  intro x hxs a ha
  by_cases hcmp : a â‰¤ 1
  Â· by_cases a = 1 <;> (try simp_all; done)
    refine he.2 (c.smul_mem âŸ¨a, haâŸ© (he.1 hxs))
      (c.smul_mem âŸ¨2, by positivityâŸ© (he.1 hxs)) hxs ?_ |>.1
    simpa using smul_mem_openSegment (ğ•œ := ğ•œ) (E := E) (b := 1) (lt_of_le_of_ne hcmp â€¹_â€º) (by simp)
  Â· refine he.2 (c.smul_mem âŸ¨0, le_rflâŸ© (he.1 hxs)) (c.smul_mem âŸ¨a, haâŸ© (he.1 hxs)) hxs ?_ |>.2
    simpa using smul_mem_openSegment (ğ•œ := ğ•œ) (E := E) (a := 0) (b := 1) (by simp) (by simp_all)

theorem smul_mem_convexHull {s : Set E} (h : âˆ€ x âˆˆ s, âˆ€ a : ğ•œ, a â‰¥ 0 â†’ a â€¢ x âˆˆ s) :
    âˆ€ x âˆˆ convexHull ğ•œ s, âˆ€ a : ğ•œ, a â‰¥ 0 â†’ a â€¢ x âˆˆ convexHull ğ•œ s := by
  let t := { x âˆˆ convexHull ğ•œ s | âˆ€ a : ğ•œ, a â‰¥ 0 â†’ a â€¢ x âˆˆ convexHull ğ•œ s }
  have : Convex ğ•œ t := by
    intro x hx y hy a b ha hb hab
    refine âŸ¨convex_convexHull ğ•œ s hx.1 hy.1 ha hb hab, fun c hc => ?_âŸ©
    rw [smul_add, smul_smul, smul_smul, mul_comm, mul_comm (b := b), mul_smul, mul_smul]
    refine convex_convexHull ğ•œ s ?_ ?_ ha hb hab
    Â· exact hx.2 c hc
    Â· exact hy.2 c hc
  intro x hx
  rw [mem_convexHull_iff] at hx
  replace hx := hx t
      (fun y hy => âŸ¨subset_convexHull ğ•œ s hy, fun a ha => subset_convexHull ğ•œ s (h y hy a ha)âŸ©) this
  exact hx.2

theorem coe_span_eq_convexHull {s : Set E} (hn : s.Nonempty)
    (h : âˆ€ x âˆˆ s, âˆ€ a : ğ•œ, a â‰¥ 0 â†’ a â€¢ x âˆˆ s) :
    span ğ•œ s = convexHull ğ•œ s := by
  obtain âŸ¨x, hxâŸ© := hn
  apply subset_antisymm
  Â· apply Submodule.span_induction
    Â· exact subset_convexHull ğ•œ s
    Â· apply subset_convexHull ğ•œ s
      simpa using h x hx 0
    Â· intro y z _ _ hy hz
      have hyâ‚‚ := smul_mem_convexHull h y hy 2 (by positivity)
      have hzâ‚‚ := smul_mem_convexHull h z hz 2 (by positivity)
      simpa using convex_convexHull ğ•œ s hyâ‚‚ hzâ‚‚ (a := 1 / 2) (b := 1 / 2)
        (by positivity) (by positivity) (by ring)
    Â· intro a x _ hx
      exact smul_mem_convexHull h x hx a.1 a.2
  Â· intro y hy
    apply mem_convexHull_iff.mp hy
    Â· exact subset_span
    Â· exact (span ğ•œ s).toConvexCone.convex

theorem coe_span_eq_convexHull' {s : Set E} (hn : s.Nonempty) :
    span ğ•œ s = convexHull ğ•œ { x | âˆƒ (r : { r : ğ•œ // 0 â‰¤ r }) (y : E), y âˆˆ s âˆ§ x = r â€¢ y } := by
  let t := { x | âˆƒ (r : { r : ğ•œ // 0 â‰¤ r }) (y : E), y âˆˆ s âˆ§ x = r â€¢ y }
  rw [â† coe_span_eq_convexHull]
  Â· simp only [SetLike.coe_set_eq]
    apply le_antisymm
    Â· rw [Submodule.span_le]
      intro x hx
      apply subset_span
      exact âŸ¨âŸ¨1, by positivityâŸ©, x, hx, by simpâŸ©
    Â· rw [Submodule.span_le]
      rintro x âŸ¨r, x, hx, rflâŸ©
      apply Submodule.smul_mem
      apply subset_span hx
  Â· obtain âŸ¨x, hxâŸ© := hn
    exact âŸ¨x, 1, x, hx, by simpâŸ©
  Â· simp only [Subtype.exists, Nonneg.mk_smul]
    rintro x âŸ¨r, hr, y, hy, rflâŸ©
    intro r' hr'
    rw [smul_smul]
    refine âŸ¨r' * r, by positivity, y, hy, rflâŸ©

theorem mem_span_iff_mem_convexHull {s : Set E} (hn : s.Nonempty) {x : E} :
    x âˆˆ span ğ•œ s â†”
      x âˆˆ convexHull ğ•œ { x | âˆƒ (r : { r : ğ•œ // 0 â‰¤ r }) (y : E), y âˆˆ s âˆ§ x = r â€¢ y } := by
  rw [â† coe_span_eq_convexHull' hn]
  rfl

theorem span_eq_of_isExtreme_of_convex {c : PointedCone ğ•œ E} {s : Set E} (hn : s.Nonempty)
    (he : IsExtreme ğ•œ c s) (hc : Convex ğ•œ s) :
    span ğ•œ s = s := by
  apply le_antisymm
  Â· apply Submodule.span_induction
    Â· exact fun _ h => h
    Â· obtain âŸ¨x, hxâŸ© := hn
      simpa using smul_mem_of_isExtreme he x hx 0
    Â· intro x y hx hy hxs hys
      have hxâ‚‚ : (2 : ğ•œ) â€¢ x âˆˆ s := smul_mem_of_isExtreme he x hxs (a := 2) (by positivity)
      have hyâ‚‚ : (2 : ğ•œ) â€¢ y âˆˆ s := smul_mem_of_isExtreme he y hys (a := 2) (by positivity)
      have := hc hxâ‚‚ hyâ‚‚ (a := 1 / 2) (b := 1 / 2) (by positivity) (by positivity) (by ring)
      simpa [smul_smul] using this
    Â· intro a y _ hys
      exact smul_mem_of_isExtreme he y hys a.1 a.2
  Â· exact Submodule.subset_span

theorem span_eq_of_isExposed [TopologicalSpace ğ•œ] [TopologicalSpace E] {c : PointedCone ğ•œ E}
    {s : Set E} (hn : s.Nonempty) (he : IsExposed ğ•œ c s) :
    span ğ•œ s = s :=
  span_eq_of_isExtreme_of_convex hn he.isExtreme (he.convex c.toConvexCone.convex)

theorem _root_.isExtreme_iff_mem_convexHull_inter_of_mem_convexHull (s t : Set E) (hc : Convex ğ•œ s) :
    IsExtreme ğ•œ s t â†” t âŠ† s âˆ§ âˆ€ g : Set E, g âŠ† s â†’ âˆ€ x âˆˆ convexHull ğ•œ g, x âˆˆ t â†’ x âˆˆ convexHull ğ•œ (g âˆ© t) := by
  constructor
  Â· intro he
    refine âŸ¨he.1, ?_âŸ©
    intro g hgs
    have hcgs := hc.convexHull_subset_iff.mpr hgs
    let s' := { x âˆˆ convexHull ğ•œ g | x âˆˆ t â†’ x âˆˆ convexHull ğ•œ (g âˆ© t) }
    have : Convex ğ•œ s' := by
      intro x hx y hy a b ha hb hab
      refine âŸ¨convex_convexHull ğ•œ _ hx.1 hy.1 ha hb hab, ?_âŸ©
      intro ht
      by_cases h : a > 0 âˆ§ b > 0
      Â· have := he.2 (hcgs hx.1) (hcgs hy.1) ht ?_
        Â· exact convex_convexHull ğ•œ _ (hx.2 this.1) (hy.2 this.2) ha hb hab
        Â· exact âŸ¨a, b, h.1, h.2, hab, rflâŸ©
      Â· simp only [not_and_or] at h
        cases h
        Â· have : a = 0 := le_antisymm (le_of_not_gt â€¹_â€º) ha
          simp_all only [le_refl, zero_add, zero_smul, one_smul, gt_iff_lt, lt_self_iff_false,
            not_false_eq_true, zero_le_one]
          exact hy.2 ht
        Â· have : b = 0 := le_antisymm (le_of_not_gt â€¹_â€º) hb
          simp_all only [le_refl, add_zero, one_smul, zero_smul, gt_iff_lt, lt_self_iff_false,
            not_false_eq_true, zero_le_one]
          exact hx.2 ht
    intro x hx
    rw [mem_convexHull_iff] at hx
    specialize hx s' (by intro y hyg; exact âŸ¨subset_convexHull ğ•œ _ hyg, fun hyt => subset_convexHull ğ•œ _ âŸ¨hyg, hytâŸ©âŸ©) this
    exact hx.2
  Â· intro h
    constructor
    Â· exact h.1
    Â· rintro x hx y hy _ hzt âŸ¨a, b, ha, hb, hab, rflâŸ©
      replace h := h.2 {x, y} ?_ _ ?_ hzt -- _ (convex_convexHull ğ•œ {x, y} (x := x) ?_)
      Â· by_cases hxt : x âˆˆ t
        Â· by_cases hyt : y âˆˆ t
          Â· exact âŸ¨hxt, hytâŸ©
          Â· rw [Set.insert_inter_of_mem hxt, Set.singleton_inter_eq_empty.mpr hyt] at h
            simp at h
            replace h : y = x := by
              rw [â† eq_sub_iff_add_eq'] at h
              rw (occs := [1]) [â† one_smul ğ•œ x] at h
              rw [â† sub_smul, â† hab, add_sub_cancel_left] at h
              replace h := congr_arg (bâ»Â¹ â€¢ Â·) h
              simpa [inv_smul_smulâ‚€ (ne_of_gt hb)] using h
            exact âŸ¨hxt, h â–¸ hxtâŸ©
        Â· by_cases hyt : y âˆˆ t
          Â· rw [Set.insert_inter_of_not_mem hxt] at h
            replace h := convexHull_mono Set.inter_subset_left h
            simp at h
            replace h : x = y := by
              rw [â† eq_sub_iff_add_eq] at h
              rw (occs := [1]) [â† one_smul ğ•œ y] at h
              rw [â† sub_smul, â† hab, add_sub_cancel_right] at h
              replace h := congr_arg (aâ»Â¹ â€¢ Â·) h
              simpa [inv_smul_smulâ‚€ (ne_of_gt ha)] using h
            exact âŸ¨h â–¸ hyt, hytâŸ©
          Â· rw [Set.insert_inter_of_not_mem hxt, Set.singleton_inter_eq_empty.mpr hyt] at h
            simp at h
      Â· rintro a (hax | hay)
        Â· exact hax â–¸ hx
        Â· exact hay â–¸ hy
      Â· exact convex_convexHull ğ•œ {x, y}
          (subset_convexHull ğ•œ _ (by simp)) (subset_convexHull ğ•œ _ (by simp))
          (a := a) (by positivity) (b := b) (by positivity) hab

theorem _root_.PointedCone.mem_span_inter_of_mem_span_of_isExtreme (c : PointedCone ğ•œ E)
    (t : Set E) (h : âˆ€ r : { r : ğ•œ // 0 â‰¤ r }, âˆ€ x âˆˆ t, r â€¢ x âˆˆ t) (he : IsExtreme ğ•œ c t) :
    t âŠ† c âˆ§ âˆ€ g : Set E, g âŠ† c â†’ âˆ€ x âˆˆ span ğ•œ g, x âˆˆ t â†’ x âˆˆ span ğ•œ (g âˆ© t) := by
  have := smul_mem_of_isExtreme he
  simp only [isExtreme_iff_mem_convexHull_inter_of_mem_convexHull c t c.toConvexCone.convex] at he
  refine âŸ¨he.1, ?_âŸ©
  intro g hgc x hxg hxt
  by_cases hg : g.Nonempty
  Â· rw [mem_span_iff_mem_convexHull hg] at hxg
    let g' := { x : E | âˆƒ r : { r : ğ•œ // 0 â‰¤ r }, âˆƒ y âˆˆ g, x = r â€¢ y }
    replace he' := he.2 g' ?_ x hxg hxt
    Â· have : g' âˆ© t âŠ† { x : E | x = 0 âˆ¨ âˆƒ r : { r : ğ•œ // 0 â‰¤ r }, âˆƒ y âˆˆ g âˆ© t, x = r â€¢ y } := by
        rintro _ âŸ¨âŸ¨r, y, hyg, rflâŸ©, hxtâŸ©
        by_cases hr : r = 0
        Â· simp_all
        Â· refine Or.inr âŸ¨r, y, âŸ¨hyg, ?_âŸ©, rflâŸ©
          specialize h (1 / r) _ hxt
          obtain âŸ¨r, hrâŸ© := r
          simp at h
          rwa [inv_smul_smulâ‚€] at h
          intro h
          apply hr
          ext
          exact h
      replace he' := convexHull_mono this he'
      by_cases h' : (g âˆ© t).Nonempty
      Â· rw [mem_span_iff_mem_convexHull h']
        convert he'
        simp only [Set.mem_inter_iff, Subtype.exists, Nonneg.mk_smul, exists_prop, iff_or_self]
        intro h
        obtain âŸ¨y, âŸ¨hyg, hytâŸ©âŸ© := h'
        exact âŸ¨0, le_rfl, y, âŸ¨hyg, hytâŸ©, by simp [h]âŸ©
      Â· simp only [Set.not_nonempty_iff_eq_empty] at h'
        simp [h'] at he'
        simpa [h'] using he'
    Â· rintro _ âŸ¨r, y, hyg, rflâŸ©
      apply Submodule.smul_mem
      exact hgc hyg
  Â· simp only [Set.not_nonempty_iff_eq_empty] at hg
    simp_all

theorem IsPolyhedral.span_eq_of_isExtreme (c : PointedCone ğ•œ E) (h : IsPolyhedral c) {s : Set E}
    (he : IsExtreme ğ•œ c s) :
    IsPolyhedral (span ğ•œ s) := by
  replace he := c.mem_span_inter_of_mem_span_of_isExtreme s ?_ he
  Â· obtain âŸ¨g, hgâŸ© := isPolyhedral_iff_eq_span c |>.mp h
    refine âŸ¨(((g : Set E) âˆª {0}) âˆ© s).toFinite.toFinset, ?_âŸ©
    apply le_antisymm
    Â· rw [span_le]
      simp only [Set.union_singleton, Set.Finite.coe_toFinset]
      intro x hx
      exact subset_span hx.2
    Â· rw [span_le]
      intro x hxs
      replace he := he.2 ((g : Set E) âˆª {0}) (hg â–¸ subset_span) x (hg â–¸ he.1 hxs) hxs
      simp_all
  Â· intro r x hx
    exact smul_mem_of_isExtreme he x hx r.1 r.2

end PointedCone
