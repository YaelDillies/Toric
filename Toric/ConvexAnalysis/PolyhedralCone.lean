/-
Copyright (c) 2025 Paul Reichert. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Paul Reichert
-/
import Mathlib.Analysis.Convex.Cone.Pointed

/-!
# Pointed cone hull and polyhedral cones

We define the pointed cone hull and what it means for a pointed cone to be polyhedral.
-/

variable {ğ•œ E : Type*} [OrderedSemiring ğ•œ] [AddCommMonoid E] [Module ğ•œ E]

open Classical

namespace PointedCone

variable (ğ•œ) in
/-- The span of a set `S` is the smallest pointed cone that contains `S`.
Pointed cones being defined as submodules over nonnegative scalars, this is exactly the
submodule span of `S` w.r.t. nonnegative scalars. -/
abbrev span (S : Set E) : PointedCone ğ•œ E :=
  Submodule.span _ S

theorem subset_span {S : Set E} :
    S âŠ† PointedCone.span ğ•œ S :=
  Submodule.subset_span

/-- A pointed cone is polyhedral if it is the convex hull of finitely many points. -/
def IsPolyhedral (c : PointedCone ğ•œ E) : Prop :=
  âˆƒ t : Finset E, c = PointedCone.span ğ•œ t

protected def IsPolyhedral.span {s : Set E} (h : s.Finite) :
    (span ğ•œ s).IsPolyhedral :=
  âŸ¨h.toFinset, by simpâŸ©

@[simp]
theorem IsPolyhedral.bot :
    (âŠ¥ : PointedCone ğ•œ E).IsPolyhedral :=
  âŸ¨{0}, by simpâŸ©

section

variable {ğ•œ E : Type*} [LinearOrderedField ğ•œ] [AddCommGroup E] [Module ğ•œ E]

/-- `âŠ¤` is a polyhedral cone in a finite dimensional vector space over a linear
ordered field. -/
theorem IsPolyhedral.top [hE : FiniteDimensional ğ•œ E] :
    (âŠ¤ : PointedCone ğ•œ E).IsPolyhedral := by
  obtain âŸ¨S,hSâŸ© := Module.finite_def.mp hE
  -- We take R to be the union of S with {-x | x âˆˆ S}
  let R : Finset E := S âˆª S.map (Function.Embedding.mk (Neg.neg : E â†’ E) neg_injective)
  -- We first show that the span of R is closed under negation
  have neg_mem_span_R : âˆ€ x âˆˆ span ğ•œ R, (-x : E) âˆˆ span ğ•œ R := by
    apply Submodule.span_induction
    Â· intro x hx
      apply Submodule.subset_span
      -- Clearly, T is closed under negation. We show this by simple case distinction
      rw [Finset.mem_coe, Finset.mem_union] at hx
      cases' hx with hxâ‚ hxâ‚‚
      Â· apply Finset.mem_union_right
        simpa only [Finset.mem_map, Function.Embedding.coeFn_mk, neg_inj, exists_eq_right]
      Â· rw [Finset.mem_map, Function.Embedding.coeFn_mk] at hxâ‚‚
        obtain âŸ¨y,hy1,rflâŸ© := hxâ‚‚
        rw [neg_neg]
        exact Finset.mem_union_left _ hy1
    -- The three other cases in the induction are trivial
    Â· rw [neg_zero]
      exact Submodule.zero_mem _
    Â· intro x y _ _ hx hy
      rw [neg_add_rev]
      exact Submodule.add_mem _ hy hx
    Â· intro t x _ hx
      rw [â†smul_neg]
      exact Submodule.smul_mem _ _ hx
  -- We now claim that `âŠ¤` is generated as a pointed cone by `R`.
  use R
  symm
  rw [Submodule.eq_top_iff']
  rw [Submodule.eq_top_iff'] at hS
  intro x
  specialize hS x
  revert hS x
  -- By reverting x, the claim now says that every element of the span of S
  -- (as a usual `â„`-submodule) is contained in the span of `R` as a pointed cone.
  -- This can be shown by induction on the span.
  apply Submodule.span_induction
  Â· intro x hxS
    apply Submodule.subset_span
    exact Finset.mem_union_left _ hxS
  Â· apply Submodule.zero_mem
  Â· intro x y _ _ hx hy
    exact Submodule.add_mem _ hx hy
  Â· intro t x _ hx
    -- This is the only interesting case, as here we have split cases
    -- according to whether the scalar `t` is positive or not.
    by_cases ht : 0 â‰¤ t
    Â· exact Submodule.smul_mem _ âŸ¨t,htâŸ© hx
    Â· rw [â†neg_neg (t â€¢ x), â†neg_smul, â†smul_neg]
      apply Submodule.smul_mem _ (âŸ¨-t, by linarithâŸ© : {a : ğ•œ // 0 â‰¤ a})
      -- We use our auxiliary statement from above
      exact neg_mem_span_R _ hx
end

end PointedCone
