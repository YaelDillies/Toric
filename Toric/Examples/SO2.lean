/-
Copyright (c) 2025 Ya√´l Dillies, Micha≈Ç Mruga≈Ça, Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies, Micha≈Ç Mruga≈Ça, Andrew Yang
-/
import Mathlib.Data.Complex.FiniteDimensional
import Mathlib.LinearAlgebra.UnitaryGroup
import Mathlib.RingTheory.AdjoinRoot
import Toric.Mathlib.RingTheory.HopfAlgebra.GroupLike
import Toric.GroupScheme.Torus
import Toric.Mathlib.Algebra.Polynomial.AlgebraMap
import Toric.Mathlib.AlgebraicGeometry.Over
import Toric.Mathlib.Data.Finsupp.Single
import Toric.Mathlib.LinearAlgebra.UnitaryGroup
import Toric.Mathlib.RingTheory.AdjoinRoot
import Toric.Mathlib.RingTheory.HopfAlgebra.GroupLike

noncomputable section

local notation3:max R "[X][Y]" => Polynomial (Polynomial R)
local notation3:max "Y" => Polynomial.C (Polynomial.X)

open Coalgebra Polynomial TensorProduct

variable {R S : Type*} [CommRing R] [CommRing S] [Algebra R S]


variable (R) in
abbrev SO2Ring : Type _ := AdjoinRoot (X ^ 2 + Y ^ 2 - 1 : R[X][Y])

namespace SO2Ring

instance : CommRing (SO2Ring R) := by delta SO2Ring; infer_instance
instance : Algebra R (SO2Ring S) := by delta SO2Ring; infer_instance
instance : IsScalarTower R S (SO2Ring S) := by delta SO2Ring; infer_instance

def mk : R[X][Y] ‚Üí‚Çê[R] SO2Ring R := Ideal.Quotient.mk‚Çê R _

nonrec def X : SO2Ring R := .mk X
nonrec def ¬´Y¬ª : SO2Ring R := .mk Y

@[simp] lemma X_def : mk .X = .X (R := R) := rfl
@[simp] lemma Y_def : mk Y = .Y (R := R) := rfl

--TODO : make equiv
def lift‚Çê (x y : S) (H : x ^ 2 + y ^ 2 = 1) : SO2Ring R ‚Üí‚Çê[R] S :=
  Ideal.Quotient.lift‚Çê _ (aevalAEval x y)
    (show Ideal.span _ ‚â§ RingHom.ker _ by simp [Ideal.span_le, Set.singleton_subset_iff, H])

@[simp]
lemma lift‚Çê_X (x y : S) (H : x ^ 2 + y ^ 2 = 1) : lift‚Çê (R := R) x y H .X = x := aevalAEval_X ..

@[simp]
lemma lift‚Çê_Y (x y : S) (H : x ^ 2 + y ^ 2 = 1) : lift‚Çê (R := R) x y H .Y = y := aevalAEval_Y ..

@[simp]
lemma relation : SO2Ring.X (R := R) ^ 2 + .Y ^ 2 = 1 := by
  simp_rw [SO2Ring.X, SO2Ring.Y, ‚Üê map_pow, ‚Üê map_add, ‚Üê map_one SO2Ring.mk]
  exact Ideal.Quotient.eq.mpr <| by simp [Ideal.mem_span_singleton]

lemma relation' : SO2Ring.X (R := R) ^ 2 = 1 - .Y ^ 2 := by simp [‚Üê relation]

@[simp] lemma relation'' : SO2Ring.X (R := R) * .X + .Y * .Y = 1 := by simp [‚Üê pow_two]

@[ext]
lemma algebraMap_ext {A : Type*} [Semiring A] [Algebra R A] {f g : SO2Ring R ‚Üí‚Çê[R] A}
    (h1 : f .X = g .X) (h2 : f .Y = g .Y) : f = g := by
  simp_rw [SO2Ring] at f g; apply Ideal.Quotient.algHom_ext; ext <;> assumption

def comulAlgHom : SO2Ring R ‚Üí‚Çê[R] SO2Ring R ‚äó[R] SO2Ring R := by
  refine lift‚Çê (.X ‚äó‚Çú .X - .Y ‚äó‚Çú .Y) (.X ‚äó‚Çú .Y + .Y ‚äó‚Çú .X) ?_
  ring_nf
  simp only [Algebra.TensorProduct.tmul_mul_tmul,
    Algebra.TensorProduct.tmul_pow (A := SO2Ring R) (B := SO2Ring R), relation', tmul_sub,
    sub_tmul, ‚Üê Algebra.TensorProduct.one_def (A := SO2Ring R) (B := SO2Ring R)]
  ring_nf

@[simp]
lemma comulAlgHom_apply_X : comulAlgHom (R := R) .X = (.X ‚äó‚Çú .X - .Y ‚äó‚Çú .Y) := by
  simp [comulAlgHom]

@[simp]
lemma comulAlgHom_apply_Y : comulAlgHom (R := R) .Y = (.X ‚äó‚Çú .Y + .Y ‚äó‚Çú .X) := by
  simp [comulAlgHom]

def counitAlgHom : SO2Ring R ‚Üí‚Çê[R] R := lift‚Çê 1 0 (by simp)

@[simp] lemma counitAlgHom.apply_X : counitAlgHom (R := R) .X = 1 := by simp [counitAlgHom]
@[simp] lemma counitAlgHom.apply_Y : counitAlgHom (R := R) .Y = 0 := by simp [counitAlgHom]

instance : Bialgebra R (SO2Ring R) := .ofAlgHom comulAlgHom counitAlgHom
  (by ext <;> simp [sub_tmul, tmul_sub, tmul_add, add_tmul] <;> ring)
  (by ext <;> simp [sub_tmul, tmul_sub, tmul_add, add_tmul])
  (by ext <;> simp [sub_tmul, tmul_sub, tmul_add, add_tmul])

@[simp] lemma comul_def : comul (R := R) (A := SO2Ring R) = comulAlgHom (R := R) := rfl
@[simp] lemma counit_def : counit (R := R) (A := SO2Ring R) = counitAlgHom (R := R) := rfl

def antipodeAlgHom : SO2Ring R ‚Üí‚Çê[R] SO2Ring R := lift‚Çê .X (-.Y) (by simp)

@[simp] lemma antipodeAlgHom_X : antipodeAlgHom (R := R) .X = X := by simp [antipodeAlgHom]
@[simp] lemma antipodeAlgHom_Y : antipodeAlgHom (R := R) .Y = -.Y := by simp [antipodeAlgHom]

instance : HopfAlgebra R (SO2Ring R) := by
  refine .ofAlgHom antipodeAlgHom ?_ <| by ext <;> simp; ring_nf
  ext
  ¬∑ simp [‚Üê sq]
  ¬∑ simp
    ring_nf

@[simp] lemma antipode_X : antipode R X = X (R := R) := antipodeAlgHom_X
@[simp] lemma antipode_Y : antipode R SO2Ring.Y = - .Y (R := R) := antipodeAlgHom_Y

private lemma foo : (SO2Ring.X (R := ‚ÑÇ)) ^ 2 - (Complex.I ‚Ä¢ SO2Ring.Y) ^ 2 = 1 := calc
  _ = .X ^ 2 - (Complex.I ‚Ä¢ .Y) * (Complex.I ‚Ä¢ .Y) := by ring
  _ = .X ^ 2 - (Complex.I) ^ 2 ‚Ä¢ .Y ^ 2 := by
    rw [Algebra.mul_smul_comm, Algebra.smul_mul_assoc, smul_smul]
    ring_nf
  _ = _ := by simp

@[simps!]
def T : GroupLike ‚ÑÇ (SO2Ring ‚ÑÇ) where
  val := .X + Complex.I ‚Ä¢ .Y
  isGroupLikeElem_val := ‚ü®by simp, by
    simp [sub_tmul, tmul_sub, tmul_add, add_tmul, ‚Üê smul_tmul', smul_smul]
    ring_nf‚ü©

private def complexEquivFun : MonoidAlgebra ‚ÑÇ (Multiplicative ‚Ñ§) ‚Üí‚Çêc[‚ÑÇ] SO2Ring ‚ÑÇ := by
  refine (MonoidAlgebra.liftGroupLikeBialgHom _ _).comp <|
    MonoidAlgebra.mapDomainBialgHom ‚ÑÇ (M := Multiplicative ‚Ñ§) <| AddMonoidHom.toMultiplicative''  <|
     zmultiplesHom _ <| .ofMul T

private lemma complexEquivFun_single (a : Multiplicative ‚Ñ§) (b : ‚ÑÇ) :
    complexEquivFun (.single a b) = b ‚Ä¢ (T ^ a.toAdd).1 := by
  simp [complexEquivFun, Algebra.ofId_apply, Algebra.smul_def]

set_option allowUnsafeReducibility true in
attribute [local semireducible] MonoidAlgebra.single in
private def complexEquivInv : SO2Ring ‚ÑÇ ‚Üí‚Çêc[‚ÑÇ] MonoidAlgebra ‚ÑÇ (Multiplicative ‚Ñ§) := by
  refine .ofAlgHom'
    (lift‚Çê
      ((1 / 2 : ‚ÑÇ) ‚Ä¢ (.single (.ofAdd 1) 1 + .single (.ofAdd (-1)) 1))
      (- (.I / 2 : ‚ÑÇ) ‚Ä¢ (.single (.ofAdd 1) 1 - .single (.ofAdd (-1)) 1)) ?_) ?_ ?_
  ¬∑ simp [pow_two, sub_mul, mul_sub, add_mul, mul_add, MonoidAlgebra.single_mul_single, ‚Üê ofAdd_add,
      ‚Üê two_nsmul, ‚Üê mul_smul, ‚Üê mul_inv_rev, div_mul_div_comm, neg_div, smul_sub,
      MonoidAlgebra.one_def]
    module
  ¬∑ ext <;> simp [MonoidAlgebra.counit_single]; norm_num
  ¬∑ ext
    ¬∑ simp [MonoidAlgebra.comul_single, smul_add, tmul_add, add_tmul, smul_sub, sub_tmul,
        tmul_sub, neg_tmul, tmul_neg, ‚Üê smul_tmul', tmul_smul, smul_smul, div_mul_div_comm,
        Complex.I_mul_I]
      module
    ¬∑ simp [MonoidAlgebra.comul_single, smul_add, tmul_add, add_tmul, smul_sub, sub_tmul, tmul_sub,
        neg_tmul, tmul_neg, ‚Üê smul_tmul', tmul_smul, smul_smul, div_mul_div_comm, Complex.I_mul_I]
      module

def complexEquiv : MonoidAlgebra ‚ÑÇ (Multiplicative ‚Ñ§) ‚âÉ‚Çêc[‚ÑÇ] SO2Ring ‚ÑÇ where
  __ := complexEquivFun
  __ : MonoidAlgebra ‚ÑÇ (Multiplicative ‚Ñ§) ‚âÉ‚Çê[‚ÑÇ] SO2Ring ‚ÑÇ := by
    refine .ofAlgHom (AlgHomClass.toAlgHom complexEquivFun) complexEquivInv ?_ ?_
    ¬∑ ext
      ¬∑ simp [complexEquivFun_single, complexEquivInv]
        module
      simp [complexEquivInv, complexEquivFun_single, ‚Üêtwo_smul, smul_smul, div_mul_eq_mul_div,
         -nsmul_eq_mul]
      module
    ¬∑ ext
      simp [complexEquivFun_single, complexEquivInv, smul_smul, mul_div, smul_sub, neg_div,
        MonoidAlgebra.single, ‚Üê sub_eq_add_neg, ‚Üê Finsupp.single_add_apply, -Finsupp.single_add]
      norm_num

@[simp] lemma complexEquiv_single (a : Multiplicative ‚Ñ§) (b : ‚ÑÇ) :
    complexEquiv (.single a b) = b ‚Ä¢ (T ^ a.toAdd).1 := complexEquivFun_single ..

@[simp] lemma complexEquiv_comp_algebraMap :
    .comp complexEquiv (algebraMap ‚ÑÇ <| MonoidAlgebra ‚ÑÇ <| Multiplicative ‚Ñ§) =
      algebraMap ‚ÑÇ (SO2Ring ‚ÑÇ) := by ext; simp [Algebra.algebraMap_eq_smul_one]

def algHomMulEquiv : (SO2Ring R ‚Üí‚Çê[R] S) ‚âÉ* Matrix.specialOrthogonalGroup (Fin 2) S where
  toFun f := ‚ü®!![f .X, f .Y; - f .Y, f .X], by
    simp [‚Üê map_mul, ‚Üê map_add, Matrix.mem_specialOrthogonalGroup_fin_two_iff, pow_two]‚ü©
  invFun M := SO2Ring.lift‚Çê (M.1 0 0) (M.1 0 1)
    (Matrix.mem_specialOrthogonalGroup_fin_two_iff.mp M.2).2.2
  left_inv f := by ext <;> simp
  right_inv M := by ext i j; fin_cases i, j <;>
    simp [(Matrix.mem_specialOrthogonalGroup_fin_two_iff.mp M.2).2.1,
      (Matrix.mem_specialOrthogonalGroup_fin_two_iff.mp M.2).1]
  map_mul' f g := by
    ext i j
    fin_cases i, j
    ¬∑ simp [sub_eq_add_neg]
    ¬∑ simp [sub_eq_add_neg]
    ¬∑ simp [sub_eq_add_neg]
    ¬∑ simp [sub_eq_neg_add]

instance : Algebra S (S ‚äó[R] SO2Ring R) :=
  Algebra.TensorProduct.leftAlgebra (A := S) (B := SO2Ring R)

variable (R S) in
def baseChangeAlgEquiv : S ‚äó[R] SO2Ring R ‚âÉ‚Çê[S] SO2Ring S := .trans
  (AdjoinRoot.tensorAlgEquiv _ _ rfl) <|
  AdjoinRoot.mapAlgEquiv _ _ (polyEquivTensor' _ _).symm (by simp)

@[simp]
lemma baseChangeAlgEquiv_X : (baseChangeAlgEquiv R S) (1 ‚äó‚Çú X) = X := by
  change (baseChangeAlgEquiv R S) (1 ‚äó‚Çú (AdjoinRoot.root _)) = AdjoinRoot.root _
  simp [baseChangeAlgEquiv]

@[simp]
lemma baseChangeAlgEquiv_Y : (baseChangeAlgEquiv R S) (1 ‚äó‚Çú ¬´Y¬ª) = ¬´Y¬ª := by
  change (baseChangeAlgEquiv R S) (1 ‚äó‚Çú (AdjoinRoot.of _ _)) = AdjoinRoot.of _ _
  simp [baseChangeAlgEquiv]

variable (R S) in
def baseChangeBialgEquiv : S ‚äó[R] SO2Ring R ‚âÉ‚Çêc[S] SO2Ring S :=
  .ofAlgEquiv' (baseChangeAlgEquiv R S)
  (by aesop)
  (by aesop (add simp [tmul_add, tmul_sub]))

@[simp]
lemma coe_baseChangeBialgEquiv : ‚áë(baseChangeBialgEquiv R S) = baseChangeAlgEquiv R S := rfl

@[simp] lemma baseChangeBialgEquiv_comp_algebraMap :
  .comp (baseChangeBialgEquiv R S) (Algebra.ofId S (S ‚äó[R] SO2Ring R)) =
    Algebra.ofId S (SO2Ring S) := by ext

end SO2Ring

open AlgebraicGeometry CategoryTheory Limits SO2Ring
open scoped Hom

namespace AlgebraicGeometry.SO‚ÇÇ

open Scheme

scoped notation3 "SO‚ÇÇ("R")" => Spec <| .of <| SO2Ring R

def so‚ÇÇComplexIso : SO‚ÇÇ(‚ÑÇ) ‚âÖ Diag Spec(‚ÑÇ) ‚Ñ§ :=
  Scheme.Spec.mapIso complexEquiv.toAlgEquiv.toRingEquiv.toCommRingCatIso.op ‚â™‚â´
    (diagSpecIso (.of ‚ÑÇ) ‚Ñ§).symm

@[simp] lemma so‚ÇÇComplexIso_hom :
    so‚ÇÇComplexIso.hom =
      ((bialgSpec <| .of ‚ÑÇ).map <| .op <| CommBialgCat.ofHom complexEquiv.toBialgHom).hom.left ‚â´
        (diagSpecIso (.of ‚ÑÇ) ‚Ñ§).inv := rfl

@[simp] lemma so‚ÇÇComplexIso_inv :
    so‚ÇÇComplexIso.inv =
      (diagSpecIso (.of ‚ÑÇ) ‚Ñ§).hom ‚â´
        ((bialgSpec <| .of ‚ÑÇ).map <| .op <|
          CommBialgCat.ofHom complexEquiv.symm.toBialgHom).hom.left := rfl

instance : so‚ÇÇComplexIso.hom.IsOver Spec(‚ÑÇ) := by rw [so‚ÇÇComplexIso_hom]; infer_instance

lemma so‚ÇÇComplexIso_hom_asOver :
    so‚ÇÇComplexIso.hom.asOver Spec(‚ÑÇ) =
      ((bialgSpec <| .of ‚ÑÇ).map <| .op <| CommBialgCat.ofHom complexEquiv.toBialgHom).hom ‚â´
        (diagSpecIso (.of ‚ÑÇ) ‚Ñ§).inv.asOver Spec(‚ÑÇ) := rfl

instance : IsMon_Hom <| so‚ÇÇComplexIso.hom.asOver Spec(‚ÑÇ) := by
  rw [so‚ÇÇComplexIso_hom_asOver]; infer_instance

instance : SO‚ÇÇ(‚ÑÇ).IsSplitTorusOver Spec(‚ÑÇ) := .of_iso so‚ÇÇComplexIso

def pullbackSO‚ÇÇRealComplex : pullback (SO‚ÇÇ(‚Ñù) ‚Üò Spec(‚Ñù)) (Spec(‚ÑÇ) ‚Üò Spec(‚Ñù)) ‚âÖ SO‚ÇÇ(‚ÑÇ) :=
  pullbackSymmetry .. ‚â™‚â´ pullbackSpecIso .. ‚â™‚â´ Scheme.Spec.mapIso
    (baseChangeBialgEquiv ‚Ñù ‚ÑÇ).symm.toAlgEquiv.toRingEquiv.toCommRingCatIso.op

@[simp] lemma pullbackSO‚ÇÇRealComplex_hom :
    pullbackSO‚ÇÇRealComplex.hom = (pullbackSymmetry .. ‚â™‚â´ pullbackSpecIso' ‚Ñù ‚ÑÇ (SO2Ring ‚Ñù)).hom ‚â´
      ((bialgSpec <| .of ‚ÑÇ).map <| .op <|
        CommBialgCat.ofHom (baseChangeBialgEquiv ‚Ñù ‚ÑÇ).symm.toBialgHom).hom.left := rfl

universe u in
instance {R S T : Type u} [CommRing R] [CommRing S] [CommRing T] [Algebra R S] [Bialgebra R T] :
    (pullbackSymmetry .. ‚â™‚â´ pullbackSpecIso' R S T).hom.IsOver Spec(S) where
  comp_over := by
    rw [‚Üê cancel_epi (pullbackSymmetry .. ‚â™‚â´ pullbackSpecIso' ..).inv,
      canonicallyOverPullback_over]
    simp [specOverSpec_over, ‚Üê Spec.map_comp, ‚Üê CommRingCat.ofHom_comp, pullbackSpecIso']
    rfl

lemma _root_.Algebra.TensorProduct.algebraMap_eq_includeLeftRingHom
    {R S T : Type*} [CommRing R] [CommRing S] [CommRing T] [Algebra R S] [Algebra R T] :
    algebraMap S (S ‚äó[R] T) = Algebra.TensorProduct.includeLeftRingHom := rfl

universe u

@[reassoc (attr := simp)]
lemma pullbackSpecIso_hom_base (R S T : Type u) [CommRing R] [CommRing S] [CommRing T] [Algebra R S]
    [Algebra R T] :
    (pullbackSpecIso R S T).hom ‚â´ Spec.map (CommRingCat.ofHom (algebraMap R _)) =
      pullback.fst _ _ ‚â´ Spec.map (CommRingCat.ofHom (algebraMap _ _)) := by
  simp [‚Üê Iso.eq_inv_comp, ‚Üê Spec.map_comp, ‚Üê CommRingCat.ofHom_comp,
    ‚Üê Algebra.TensorProduct.algebraMap_eq_includeLeftRingHom, ‚Üê IsScalarTower.algebraMap_eq]

@[reassoc (attr := simp)]
lemma pullbackSpecIso_hom_fst' (R S T : Type u) [CommRing R] [CommRing S] [CommRing T] [Algebra R S]
    [Algebra R T] :
    (pullbackSpecIso R S T).hom ‚â´ Spec.map (CommRingCat.ofHom (algebraMap S _)) =
      pullback.fst _ _ := by
  simp [‚Üê Iso.eq_inv_comp, pullbackSpecIso_inv_fst,
    ‚Üê Algebra.TensorProduct.algebraMap_eq_includeLeftRingHom]

@[reassoc (attr := simp)]
lemma pullbackSpecIso_inv_fst' (R S T : Type u) [CommRing R] [CommRing S] [CommRing T] [Algebra R S]
    [Algebra R T] :
    (pullbackSpecIso R S T).inv ‚â´ pullback.fst _ _ =
    Spec.map (CommRingCat.ofHom (algebraMap S _)) := by
  simp [‚Üê cancel_epi (pullbackSpecIso R S T).hom]

@[reassoc (attr := simp)]
lemma pullbackSpecIso_hom_snd' (R S T : Type u) [CommRing R] [CommRing S] [CommRing T] [Algebra R S]
    [Algebra R T] :
    (pullbackSpecIso R S T).hom ‚â´ Spec.map (CommRingCat.ofHom
      (Algebra.TensorProduct.includeRight (R := R) (A := S) (B := T) : _ ‚Üí+* _)) =
      pullback.snd _ _ := by
  simp [‚Üê Iso.eq_inv_comp, pullbackSpecIso_inv_fst,
    ‚Üê Algebra.TensorProduct.algebraMap_eq_includeLeftRingHom]

attribute [local instance] Over.cartesianMonoidalCategory  in
open MonoidalCategory in
@[reassoc (attr := simp)]
lemma Over.tensorHom_left_fst' {C : Type*} [Category C] [HasPullbacks C]
    {X S U : C} {fS : S ‚ü∂ X} {fU : U ‚ü∂ X} {R T : Over X} (f : R ‚ü∂ .mk fS) (g : T ‚ü∂ .mk fU) :
    (tensorHom f g).left ‚â´ pullback.fst fS fU = pullback.fst _ _ ‚â´ f.left :=
  limit.lift_œÄ _ _

lemma pullbackSpecIso'_symmetry {R S T: Type u} [CommRing R] [CommRing S] [CommRing T]
    [Algebra R S] [Bialgebra R T] : (pullbackSymmetry .. ‚â™‚â´ pullbackSpecIso' R S T).hom =
    (pullbackSpecIso' ..).hom ‚â´
    Spec.map (CommRingCat.ofHom (Algebra.TensorProduct.comm R S T)) := by
  simp_rw [Iso.trans_hom, ‚Üê Iso.eq_comp_inv, Category.assoc, ‚Üê Iso.inv_comp_eq]
  ext
  ¬∑ have : (RingHomClass.toRingHom (Algebra.TensorProduct.comm R S T)).comp
      Algebra.TensorProduct.includeLeftRingHom = Algebra.TensorProduct.includeRight.toRingHom := rfl
    simp [specOverSpec_over, pullbackSpecIso', ‚Üê Spec.map_comp, ‚Üê CommRingCat.ofHom_comp, this]
  have : (RingHomClass.toRingHom (Algebra.TensorProduct.comm R S T)).comp
      (RingHomClass.toRingHom Algebra.TensorProduct.includeRight) =
      Algebra.TensorProduct.includeLeftRingHom := rfl
  simp [specOverSpec_over, pullbackSpecIso', ‚Üê Spec.map_comp, ‚Üê CommRingCat.ofHom_comp, this]

example {C : Type*} [Category C] {A B : C} {f : A ‚ü∂ B} : f ‚â´ (ùüô _) = f := Category.comp_id f
--- (f a).1 = f a.1
lemma foo (R S T : Type u) [CommRing R] [CommRing S] [CommRing T] [Algebra R S] [Bialgebra R T] :
      (Functor.LaxMonoidal.Œº (Over.pullback (Spec.map (CommRingCat.ofHom (algebraMap R S))))
        (Over.mk (Spec.map (CommRingCat.ofHom (algebraMap R T))))
        (Over.mk (Spec.map (CommRingCat.ofHom (algebraMap R T))))).left ‚â´
    pullback.fst _ _ ‚â´
      (pullbackSpecIso R T T).hom =
    (MonoidalCategoryStruct.tensorHom
        ((pullbackSymmetry .. ‚â™‚â´ pullbackSpecIso' R S T).hom.asOver (Spec(S)))
        ((pullbackSymmetry .. ‚â™‚â´ pullbackSpecIso' R S T).hom.asOver (Spec(S)))).left ‚â´
    (pullbackSpecIso S (S ‚äó[R] T) (S ‚äó[R] T)).hom ‚â´
    Spec.map (CommRingCat.ofHom (Algebra.TensorProduct.mapRingHom (algebraMap _ _)
      Algebra.TensorProduct.includeRight.toRingHom
      Algebra.TensorProduct.includeRight.toRingHom
      (by simp [‚Üê IsScalarTower.algebraMap_eq])
      (by simp [‚Üê IsScalarTower.algebraMap_eq]))) := by
  rw [‚Üê cancel_mono (pullbackSpecIso ..).inv]
  simp
  ext <;> simp
  ¬∑ simp only [‚Üê Spec.map_comp, ‚Üê CommRingCat.ofHom_comp,
      Algebra.TensorProduct.mapRingHom_comp_includeLeftRingHom]
    simp [specOverSpec_over]
    erw [Over.tensorHom_left_fst_assoc]
    simp [pullbackSpecIso']
    rfl
  ¬∑ simp only [‚Üê Spec.map_comp, ‚Üê CommRingCat.ofHom_comp,
      Algebra.TensorProduct.mapRingHom_comp_includeRight]
    simp [specOverSpec_over]
    erw [Over.tensorHom_left_snd_assoc]
    simp [pullbackSpecIso']
    rfl

-- TODO : maybe refactor counitAlgHom/comulAlgHom to take in 3 arguments, if you care
@[simp]
theorem _root_.Bialgebra.counitAlgHom_comp_includeRight
  {R S T : Type u} [CommRing R] [CommRing S] [CommRing T] [Algebra R S] [Bialgebra R T] :
    ((Bialgebra.counitAlgHom S (S ‚äó[R] T)).restrictScalars R).comp
    Algebra.TensorProduct.includeRight =
    (Algebra.ofId R S).comp (Bialgebra.counitAlgHom R T)
     := by
  ext
  simp [Algebra.ofId_apply, Algebra.algebraMap_eq_smul_one]

theorem name1
  {R S T : Type u} [CommRing R] [CommRing S] [CommRing T] [Algebra R S] [Bialgebra R T] :
    (RingHomClass.toRingHom (Bialgebra.comulAlgHom S (S ‚äó[R] T))).comp
    (RingHomClass.toRingHom (Algebra.TensorProduct.includeRight (R := R) (A := S) (B := T))) =
    (Algebra.TensorProduct.mapRingHom (algebraMap R S)
    (RingHomClass.toRingHom (Algebra.TensorProduct.includeRight (R := R) (A := S) (B := T)))
    (RingHomClass.toRingHom (Algebra.TensorProduct.includeRight (R := R) (A := S) (B := T)))
    (by simp; rfl)
    (by simp; rfl)).comp
    (RingHomClass.toRingHom (Bialgebra.comulAlgHom R T)) := by
  ext x
  simp [‚Üê (‚Ñõ R x).eq, tmul_sum]

instance {R S T : Type u} [CommRing R] [CommRing S] [CommRing T] [Algebra R S] [Bialgebra R T] :
    IsMon_Hom <| (pullbackSymmetry .. ‚â™‚â´ pullbackSpecIso' R S T).hom.asOver Spec(S) where
  one_hom := by
    ext
    rw [‚Üê cancel_mono (pullbackSpecIso' ..).inv]
    ext
    ¬∑ simp [mon_ClassAsOverPullback_one, algSpec_Œµ_left (R := CommRingCat.of _),
      pullbackSpecIso', specOverSpec_over, ‚Üê Spec.map_comp, ‚Üê CommRingCat.ofHom_comp,
      ‚Üê Algebra.TensorProduct.algebraMap_eq_includeLeftRingHom]
    ¬∑ simp [mon_ClassAsOverPullback_one, algSpec_Œµ_left (R := CommRingCat.of _),
        pullbackSpecIso', specOverSpec_over, ‚Üê Spec.map_comp, ‚Üê CommRingCat.ofHom_comp,
        ‚Üê Algebra.TensorProduct.algebraMap_eq_includeLeftRingHom,
        ‚Üê AlgHom.coe_restrictScalars R (Bialgebra.counitAlgHom S _), - AlgHom.coe_restrictScalars,
        ‚Üê AlgHom.comp_toRingHom, Bialgebra.counitAlgHom_comp_includeRight]
      rfl
  mul_hom := by
    ext
    rw [‚Üê cancel_mono (pullbackSpecIso' ..).inv]
    ext
    ¬∑ simp [mon_ClassAsOverPullback_mul, pullbackSpecIso', specOverSpec_over, ‚Üê Spec.map_comp,
        ‚Üê CommRingCat.ofHom_comp, asOver, OverClass.asOver, AlgebraicGeometry.Scheme.mul_left,
        ‚Üê Algebra.TensorProduct.algebraMap_eq_includeLeftRingHom, Hom.asOver, OverClass.asOverHom,
        pullback.condition]
      rfl
    ¬∑ convert congr($(foo R S T) ‚â´
        Spec.map (CommRingCat.ofHom (Bialgebra.comulAlgHom R T).toRingHom)) using 1
      ¬∑ simp [mon_ClassAsOverPullback_mul, pullbackSpecIso', specOverSpec_over, OverClass.asOver,
          Hom.asOver, OverClass.asOverHom, mul_left]
      ¬∑ simp [mon_ClassAsOverPullback_mul, pullbackSpecIso', specOverSpec_over, OverClass.asOver,
          Hom.asOver, OverClass.asOverHom, mul_left, ‚Üê Spec.map_comp, ‚Üê CommRingCat.ofHom_comp,
          name1]


-- generalize this
instance : (pullbackSymmetry .. ‚â™‚â´ pullbackSpecIso' ‚Ñù ‚ÑÇ (SO2Ring ‚Ñù)).hom.IsOver Spec(‚ÑÇ) where
  comp_over := by
    rw [‚Üê cancel_epi (pullbackSymmetry .. ‚â™‚â´ pullbackSpecIso' ..).inv, canonicallyOverPullback_over]
    simp [specOverSpec_over, pullbackSpecIso']

instance : pullbackSO‚ÇÇRealComplex.hom.IsOver Spec(‚ÑÇ) := by
  rw [pullbackSO‚ÇÇRealComplex_hom]; infer_instance

@[simp] lemma pullbackSO‚ÇÇRealComplex_hom_asOver :
    pullbackSO‚ÇÇRealComplex.hom.asOver Spec(‚ÑÇ) =
      (pullbackSymmetry .. ‚â™‚â´ pullbackSpecIso' ‚Ñù ‚ÑÇ (SO2Ring ‚Ñù)).hom.asOver Spec(‚ÑÇ) ‚â´
        ((bialgSpec <| .of ‚ÑÇ).map <| .op <|
          CommBialgCat.ofHom (baseChangeBialgEquiv ‚Ñù ‚ÑÇ).symm.toBialgHom).hom := rfl

instance : IsMon_Hom <| pullbackSO‚ÇÇRealComplex.hom.asOver Spec(‚ÑÇ) := by
  rw [pullbackSO‚ÇÇRealComplex_hom_asOver]; infer_instance

instance pullback_SO‚ÇÇ_real_isSplitTorusOver_complex :
    (pullback (SO‚ÇÇ(‚Ñù) ‚Üò Spec(‚Ñù)) (Spec(‚ÑÇ) ‚Üò Spec(‚Ñù))).IsSplitTorusOver Spec(‚ÑÇ) :=
  .of_iso pullbackSO‚ÇÇRealComplex

instance : Spec(SO2Ring ‚Ñù).IsTorusOver ‚Ñù where
  existsSplit :=
    ‚ü®‚ÑÇ, inferInstance, inferInstance, inferInstance, pullback_SO‚ÇÇ_real_isSplitTorusOver_complex‚ü©

instance {R S T : Type u} [CommRing R] [CommRing S] [CommRing T] [Algebra R S]
    [Algebra R T] (f : S ‚Üí‚Çê[R] T) : (Spec.map (CommRingCat.ofHom f.toRingHom)).IsOver Spec(R) where
  comp_over := by
    simp [specOverSpec_over, ‚Üê Spec.map_comp, ‚Üê CommRingCat.ofHom_comp]

def Spec.mulEquiv {R S T : Type u} [CommRing R] [CommRing S] [CommRing T] [Bialgebra R S]
    [Algebra R T] :
    (S ‚Üí‚Çê[R] T) ‚âÉ* (Spec(T).asOver Spec(R) ‚ü∂ Spec(S).asOver Spec(R)) where
  toFun f := (Spec.map (CommRingCat.ofHom f.toRingHom)).asOver _
  invFun f := ‚ü®(Spec.preimage f.left).hom, by
    suffices CommRingCat.ofHom (algebraMap R S) ‚â´ Spec.preimage f.left =
      CommRingCat.ofHom (algebraMap R T) from fun r ‚Ü¶ congr($this r)
    apply Spec.map_injective
    simpa [-comp_over] using f.w‚ü©
  left_inv f := by
    apply AlgHom.coe_ringHom_injective
    simp
  right_inv f := by ext1; simp
  map_mul' f g := by
    ext1
    dsimp [AlgHom.mul_def, AlgHom.comp_toRingHom, Hom.mul_def]
    simp only [‚Üê Category.assoc, Spec.map_comp, AlgebraicGeometry.Scheme.mul_left]
    congr 1
    rw [‚Üê Iso.comp_inv_eq]
    ext <;> simp only [specOverSpec_over, ‚Üê Spec.map_comp, ‚Üê CommRingCat.ofHom_comp,
      ‚Üê AlgHom.comp_toRingHom, Category.assoc, pullbackSpecIso_inv_fst, pullbackSpecIso_inv_snd,
      limit.lift_œÄ, PullbackCone.mk_pt, PullbackCone.mk_œÄ_app]
    ¬∑ congr 3
      ext; simp
    ¬∑ congr 3
      ext; simp

def bar : (Spec(R).asOver Spec(R) ‚ü∂ SO‚ÇÇ(R).asOver Spec(R)) ‚âÉ*
    Matrix.specialOrthogonalGroup (Fin 2) R :=
  Spec.mulEquiv.symm.trans (algHomMulEquiv (R := R))


def SplitTorus.mulEquiv {R : CommRingCat.{u}} (œÉ : Type u) :
    (œÉ ‚Üí RÀ£) ‚âÉ* ((Spec R).asOver (Spec R) ‚ü∂ (SplitTorus (Spec R) œÉ).asOver (Spec R)) := by
  sorry

def I : Matrix.specialOrthogonalGroup (Fin 2) ‚Ñù :=
  ‚ü®!![0, 1; -1, 0], by simp [Matrix.mem_specialOrthogonalGroup_fin_two_iff]‚ü©

lemma I_sq_ne_1 : I ^ 2 ‚â† 1 := by
  intro h
  have := congr_fun‚ÇÇ congr(($h).val) 0 0
  norm_num [I, pow_two] at this

@[simp]
lemma I_ord_4' : I ^ 4 = 1 := by
  simp [I, pow_succ, Matrix.one_fin_two]

private lemma aux1 {x : ‚Ñù} : x ^ 4 = 1 -> x ^ 2 = 1 := by
  intro h
  have : x ^ 4 = (x ^ 2) ^ 2 := by rw [‚Üê pow_mul]
  rw [this] at h
  rw [sq_eq_one_iff] at h
  cases h with
  | inl h => assumption
  | inr h =>
    have : 0 ‚â§ x ^ 2 := sq_nonneg x
    linarith

private lemma aux2 {œÉ : Type*} {x : œÉ ‚Üí ‚ÑùÀ£} : x ^ 4 = 1 ‚Üí x ^ 2 = 1 := by
  intro h
  ext y
  have := congr_fun h y
  simp only [Pi.pow_apply, Units.val_pow_eq_pow_val, Pi.one_apply, Units.val_one]
  have := congr(($this).val)
  simp at this
  exact aux1 this

example (œÉ : Type*) : IsEmpty <| Matrix.specialOrthogonalGroup (Fin 2) ‚Ñù ‚âÉ* (œÉ ‚Üí ‚ÑùÀ£) := by
  constructor
  intro e
  have h‚ÇÅ : (e I) ^ 4 = 1 := by simp [‚Üê map_pow]
  have h‚ÇÇ : (e I) ^ 2 ‚â† 1 := by
    rw [‚Üê map_pow, ‚Üê e.map_one, e.injective.ne_iff]
    exact I_sq_ne_1
  exact h‚ÇÇ <| aux2 h‚ÇÅ

end AlgebraicGeometry.SO‚ÇÇ
